use ::BBjWidget/BBjWidget.bbj::BBjWidget
use ::WebKit/model/Menu.bbj::MenuItem
use ::WebKit/util/DynamicLoader.bbj::DynamicLoader
use ::WebKit/util/Util.bbj::Util

use java.util.HashMap

class public EmbedPanel extends BBjWidget

    field private BBjHtmlView HtmlView!
    field private MenuItem MenuItem!
    field private String baseUri!
    
    
    method public EmbedPanel(BBjWindow wnd!)
        DynamicLoader.addLocalCSS("WebKit/framework/EmbedPanel/EmbedPanel.css")
        #super!.create(wnd!,wnd!.getAvailableControlID())
    methodend

    method public EmbedPanel(BBjWindow wnd!, BBjInt id!)
        DynamicLoader.addLocalCSS("WebKit/framework/EmbedPanel/EmbedPanel.css")
        #super!.create(wnd!,id!)
    methodend

    rem /**
    rem  * @Override
    rem  * This method is called whenever the widget needs to be rendered
    rem  * @param Boolean f_init!: if TRUE the control is rendered for the first time so this method has to perform initial rendering
    rem  */
    method public void redraw(Boolean f_init!)
        
        declare BBjChildWindow canvas!
        
        
        if f_init!>0 then
        
            tmp$ = BBjAPI().getSysGui().executeScript("window.location.href")
            #baseUri!=tmp$(1,pos("/"=tmp$,1,3))
        
            canvas! = #getCanvas()    

            #HtmlView! = canvas!.addHtmlView(101,0,0,300,300,"TEST")
            #HtmlView!.setStyle("border-style","none !important")
            #HtmlView!.addStyle("embedframe")    
            #HtmlView!.setRaisedEdge(0)
            #HtmlView!.setClientEdge(0)
        FI
        
    methodend
    
    method public void start(MenuItem MenuItem!)
        
        #MenuItem! = MenuItem!
        pgm$ = MenuItem!.getProgram()
        title$= MenuItem!.getCaption()
        menuParameters!= MenuItem!.getMenuParameters()
        queryParameters!=#loadQueryParams()
        routeParameters!=#loadRouteParams(MenuItem!.getPotentialRouteParameters())
        starttype = MenuItem!.getStartType()
       
        if pgm$>"" then
            if starttype=1 then           
                    #loadBUI(title$,pgm$,menuParameters!,queryParameters!,routeParameters!)
            else
                #loadDWC(title$,pgm$)
            fi
        fi
    methodend
    
    method public HashMap loadQueryParams()
    declare HashMap params! 
    params! = new HashMap()
    i = 0 
    while 1
        declare BBjString param! 
        param! = ""
        param! = info(7,i,err=*next)
        if param! <> ""
            params!.put(param!.substring(0,param!.lastIndexOf("=") ),param!.substring(param!.lastIndexOf("=") + 1))
            
        else
            
            break
        fi
        i = i + 1
    wend
    methodret params!
    
    methodend
    
    method public HashMap loadRouteParams(BBjVector keys!)
    declare HashMap params! 
    params! = new HashMap()
    for i = 0 to keys!.size() - 1
        declare auto String key!
        declare auto String value!
        key! = keys!.get(i)
        value! = Util.getPathParameter(key!)
        if value! <> null()
            params!.put(key!,value!)
        fi
    next i 
    methodret params!
    methodend
    
    method public void loadBUI(BBjString title$,BBjString pgm$, HashMap menuParameters!, HashMap queryParameters!,HashMap routeParameters!)
        
        appname$=#publishStub()
        uid$ = java.util.UUID.randomUUID().toString()     
        declare HashMap params! 
        params! = new HashMap()
        
        params!.put("_program",pgm$)
        params!.put("_menuParams",menuParameters!)
        params!.put("_queryParams",queryParameters!)
        params!.put("_routeParams",routeParameters!)
        
        url$=#baseUri!+"apps/"+appname$+"?launch="+uid$
        params!.put("_completeUrl",url$)
        
        BBjAPI().getGlobalNamespace().setValue(uid$,params!)
        
        BBjAPI().getGlobalNamespace().setCallbackForVariableChange(uid$,#this!,"onEmbeddedCommand")
        #HtmlView!.setUrl(url$)
    methodend    

    method public void loadDWC(BBjString title$,BBjString pgm$)
        appname$=#publishStub()
        uid$ = java.util.UUID.randomUUID().toString()
        url$=#baseUri!+"webapp/"+appname$+"?launch="+uid$ 
        BBjAPI().getGlobalNamespace().setValue(uid$,pgm$)
        BBjAPI().getGlobalNamespace().setCallbackForVariableChange(uid$,#this!,"onEmbeddedCommand")
        #HtmlView!.setUrl(url$)
    methodend    
    
    
    method private BBjString publishStub()
        source$=BBjAPI().getFileSystem().resolvePath("WebKit/framework/EmbedPanel/EmbedPanel.bbj")
        path$=dsk("")+dir("")
        config$ = BBjAPI().getConfig().getCurrentCommandLineObject().getConfigFile()
        classpath! = BBjAPI().getConfig().getCurrentCommandLineObject().getOriginalClasspathName()
        
        
        appname$ = "EmbedPanel"

        bbjHome! = System.getProperty("basis.BBjHome")
        
        appServer! = bbjapi().getAdmin("admin","admin123").getWebAppServer()
        appConfig! = appServer!.makeEmptyAppConfig()
        appConfig!.setProgramName(source$)
        appConfig!.setDwcEnabled(1)
        appConfig!.setBuiEnabled(1)
        appConfig!.setConfigFile(config$)
        if classpath! <> null() then
            appConfig!.setClasspathName(classpath!)
        fi
        appConfig!.setWorkingDirectory(path$)
        app! = appConfig!.buildApplication(appname$)
        published = appServer!.isPublished(app!)
        if (published) then
            appServer!.unpublish(appname$)
        fi
        appServer!.publish(app!)        
        methodret appname$
    methodend
    
    method public void onEmbeddedCommand(BBjNamespaceEvent ev!)
        command$ = ev!.getNewValue()
        switch command$
            case "close"
                #fireEvent(BBjAPI.ON_CLOSE,#MenuItem!)
                #destroy()
        swend
    methodend
        
classend




rem launch stub for program

REM later we'll need error handling
rem seterr launch_err

uid$ = clientenv("launch")
close$ = clientenv("close",err=*next)
if close$>"" then goto close_app

pgm$ = cast(HashMap,BBjAPI().getGlobalNamespace().getValue(uid$)).get("_program")

REM later we need to clean up
REM BBjAPI().getGlobalNamespace().removeValue(launch_id$)

bui! = BBjAPI().getBuiManager()

appname$=bui!.getApplicationName()

REM action! = bui!.appAction(appname$)
tmp$ = BBjAPI().openSysGui("X0").executeScript("window.location.href")
tmp$=tmp$(1,pos("/"=tmp$,1,3))
action! = bui!.urlAction(tmp$+"apps/"+appname$+"?close=1&launch="+uid$)
bui!.setEndAction(action!)


run pgm$

launch_err:
    a=msgbox("error launching")

close_app:
    BBjAPI().getGlobalNamespace().setValue(uid$,"close")
release
