use ::BBjWidget/BBjWidget.bbj::BBjWidget

use ::WebKit/widgets/InputField/InputField.bbj::InputField

class public LoginWidget extends BBjWidget 
    
    field private BBjChildWindow window!
    
    field public static BBjNumber ON_LOGIN = 800001
    
    field private InputField password! 
    
    method public LoginWidget(BBjWindow parent!)
        #window! = parent!.addChildWindow(parent!.getAvailableControlID(),0,0,0,0,"",$00100800$,BBjAPI().getSysGui().getAvailableContext())
        #window!.addPanelStyle("loginWidgetPanelStyle")
        #setCanvas(#window!)
        #redraw(1)
    methodend
    
    
    method public void redraw(Boolean init!)
        declare BBjChildWindow content!
        declare BBjImageCtrl logo!
        declare InputField username!
        declare BBjButton loginBtn!
        declare BBjCheckBox rememberMe!
        declare BBjChildWindow linkWrapper!
        declare BBjStaticText forgotPw!
        
        content! = #window!.addChildWindow(#window!.getAvailableControlID(),0,0,0,0,"",$00100800$,BBjAPI().getSysGui().getAvailableContext())
        content!.setDockStyle("height","calc(531px - 112px)")
        content!.setDockStyle("padding","56px 44px")
        content!.addPanelStyle("loginWidgetContentPanelStyle")
        
        logo! = content!.addImageCtrl(content!.getAvailableControlID(),0,0,0,0,"WebKit/demo/assets/prodin_logo.png")
        logo!.setStyle("height","fit-content")
        logo!.setStyle("width","fit-content")
        
        username! = new InputField(content!,content!.getAvailableControlID(),"WebKit/demo/assets/user.png")
        username!.setCallback(InputField.ON_SUFFIX_PRESSED,#this!,"onUserIconPressed")
        username!.setLabel("Username")
        username!.setPlaceHolder("name@company.com")
        username!.setDockStyle("margin-top","50px")
        
        #password! = new InputField(content!,content!.getAvailableControlID(),"WebKit/demo/assets/eye.png",1)
        #password!.setCallback(InputField.ON_SUFFIX_PRESSED,#this!,"onShowPasswordIconPressed")
        #password!.setLabel("Password")
        #password!.setPlaceHolder("Your Password")
        #password!.setDockStyle("margin-top","28px")
        
        rememberMe! = content!.addCheckBox(content!.getAvailableControlID(),0,0,0,0,"Keep me signed in")
        rememberMe!.setStyle("margin-top","32px")
        rememberMe!.addStyle("loginWidgetCheckBoxStyle")
        rememberMe!.addStyle("loginWidgetCheckBoxLabel")
        rememberMe!.addStyle("loginWidgetCheckBoxInputWrapper")
        rememberMe!.addStyle("loginWidgetCheckBoxControl")
        REM TODO use theming or ask sebatian how he did this in the p360
REM         rememberMe!.addStyle("loginWidgetCheckBoxIcon")
        
        loginBtn! = content!.addButton(content!.getAvailableControlID(),0,0,0,0,"Login")
        loginBtn!.setCallback(BBjAPI.ON_BUTTON_PUSH,#this!,"onLoginBtnPressed")
        loginBtn!.setStyle("margin-top","40px")
        loginBtn!.addStyle("loginWidgetBtnStyle")
        loginBtn!.addStyle("loginWidgetBtnLabel")
        loginBtn!.addStyle("loginWidgetBtnControl")
        
        linkWrapper! = content!.addChildWindow(content!.getAvailableControlID(),0,0,0,0,"",$00100800$,BBjAPI().getSysGui().getAvailableContext())
        linkWrapper!.setCallback(BBjAPI.ON_MOUSE_DOWN,#this!,"forgotPassword")
        linkWrapper!.setDockStyle("margin-top","16px")
        linkWrapper!.setDockStyle("justify-self","center")
        linkWrapper!.setDockStyle("height","fit-content")
        linkWrapper!.setDockStyle("width","fit-content")
        
        forgotPW! = linkWrapper!.addStaticText(linkWrapper!.getAvailableControlID(),0,0,0,0,"Forgot your password?")
        forgotPW!.addStyle("loginWidgetForgotPWStlye")
    methodend
    
    method public void onUserIconPressed(BBjCustomEvent event!)
        a = msgbox("UserIcon Pressed")
    methodend
    
    method public void onShowPasswordIconPressed(BBjCustomEvent event!)
        if #password!.isPasswordVisible() then 
            #password!.setPasswordVisible(0)
            #password!.setSuffixIcon("WebKit/demo/assets/eye.png")
        else
            #password!.setPasswordVisible(1)
            #password!.setSuffixIcon("WebKit/demo/assets/eye-off.png")
        endif    
    methodend
    
    method public void onLoginBtnPressed(BBjButtonPushEvent event!)
        #fireEvent(#ON_LOGIN,"")
    methodend
    
    method public void forgotPassword(BBjMouseDownEvent event!)
        a = msgbox("Forgot Password")
    methodend
classend
