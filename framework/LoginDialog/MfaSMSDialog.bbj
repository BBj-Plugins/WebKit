class public MfaSMSDialog

    field private String number!
    field private BBjTopLevelWindow window!    
   
    method private MfaSMSDialog()
    methodend
    
    method public MfaSMSDialog(String number!)
        #number! = number!
    methodend

    method public Boolean doModal()

            sysgui! =BBjAPI().openSysGui("X0")
            
            window! = sysgui!.addWindow(sysgui!.getAvailableContext(),25,25,1200,750,"MFA Token Required",$01101083$)
            window!.addStyle("wallpaper")
            #window! = CAST(BBjTopLevelWindow, window!)
            
            
            logo! = window!.addChildWindow(101,0,0,200,80,"",$00100800$,sysgui!.getAvailableContext())
            logo!.addPanelStyle("loginlogo")
            
            
            ctxt = sysgui!.getAvailableContext()
            main! = window!.addChildWindow(132,0,0,100,80,"",$00100800$,sysgui!.getAvailableContext())
            main!.addStyle("loginframe")
            main!.setClientEdge(0)
            main!.setNoEdge(1)    
            
            main!.addStyle("logincontainer")
            
            
            st! = main!.addStaticText(main!.getAvailableControlID(),0,0,0,0,"<html><h2>Security Token Sent</h2></html>")
            st!.setStyle("grid-column","1 / span 2")
            st!.setStyle("grid-row","1")

            number$="*******"+#number!.substring(#number!.length()-3)
            st! = main!.addStaticText(main!.getAvailableControlID(),0,0,0,0,"<html>An SMS-token has been sent to "+number$+". Please enter it below.</html>")
            st!.setStyle("grid-column","1 / span 2")
            st!.setStyle("grid-row","2")



            
            st! = main!.addStaticText(main!.getAvailableControlID(),0,0,0,0,"Token:")
            st!.setStyle("grid-column","1")
            st!.setStyle("grid-row","4")
            
            ipn_token! = main!.addInputE(main!.getAvailableControlID(),0,0,250,25,"","000 000")
            ipn_token!.setStyle("grid-column","2")
            ipn_token!.setStyle("grid-row","4")

            err_msg! = main!.addStaticText(main!.getAvailableControlID(),0,0,0,0,"<html>Token Invalid!</html>",$0010$)
            err_msg!.addStyle("errormsg")
            err_msg!.setStyle("grid-column","1 / span 2")
            err_msg!.setStyle("grid-row","6")            
            
            btn_login! = main!.addButton(1,0,0,250,25,"Verify and Continue")
            btn_login!.setAttribute("theme","primary")
            btn_login!.setAttribute("size","xl")
            btn_login!.setStyle("grid-column","1 / span 2")
            btn_login!.setStyle("grid-row","8")
            btn_login!.addStyle("loginbtn")
            btn_login!.setCallback(BBjAPI().ON_BUTTON_PUSH,"login")
            
 
            ipn_token!.focus()
            
            process_events
            
            login:
                token$=ipn_token!.getText()
                while len(token$)<6
                    token$="0"+token$
                wend
                
                curr_token$ = "574327"
            
                if curr_token$ = token$ then
                    #window!.destroy()
                    methodret Boolean.TRUE
                else
                    err_msg!.setVisible(1)
                fi
                return
                
    methodend

classend