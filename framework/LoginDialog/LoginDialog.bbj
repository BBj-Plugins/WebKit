use ::BBjWidget/BBjWidget.bbj::BBjWidget
use ::WebKit/util/DynamicLoader.bbj::DynamicLoader
use ::WebKit/widgets/InputField/InputField.bbj::InputField
use ::WebKit/framework/LoginDialog/SampleAuthProvider.bbj::SampleAuthProvider
use ::AuthKit/profile/GoogleAccountProfile.bbj::GoogleAccountProfile
use ::AuthKit/profile/MicrosoftAccountProfile.bbj::MicrosoftAccountProfile
use ::AuthKit/profile/AccountProfile.bbj::AccountProfile
use ::AuthKit/widgets/GoogleLoginWidget.bbj::GoogleLoginWidget
use ::AuthKit/widgets/MicrosoftLoginWidget.bbj::MicrosoftLoginWidget
use ::AuthKit/totp/TOTP.bbj::TOTP
use ::WebKit/framework/LoginDialog/IAuthProvider.bbj::IAuthProvider

class public LoginDialog

    field public static BBjNumber ON_LOGIN = 800001
    field private IAuthProvider authProvider!
    field private BBjTopLevelWindow window!
    field private BBjChildWindow loginWrapper!
    
    field private BBjSysGui sysGui!
    field public BBjString User$ = ""
    field public Boolean AllowRemember! = Boolean.FALSE
    
    field public Boolean LoginWithMicrosoft! = Boolean.FALSE
    field public Boolean LoginWithGoogle! = Boolean.FALSE

    field private InputField password! 
    field private InputField username! 
    field private AccountProfile Profile!
    field private BBjString logoURL$
    

    method public LoginDialog()
    methodend

        
    method public LoginDialog(BBjString title$, IAuthProvider authProvider!)
      
        #authProvider! = authProvider!
        bbjutils! = new ::BBUtils.bbj::BBUtils()
        DynamicLoader.addLocalCSS("WebKit/framework/LoginDialog/LoginDialog.css")

        #sysGui! = BBjAPI().openSysGui("X0")
        #window! = #sysGui!.addWindow(#sysGui!.getAvailableContext(),25,25,1200,750, title$, $01101083$)
        #window!.addPanelStyle("loginWrapperPanelStyle")

        #loginWrapper! = #window!.addChildWindow(#window!.getAvailableControlID(),0,0,0,0,"",$00100800$,BBjAPI().getSysGui().getAvailableContext())
        #loginWrapper!.addPanelStyle("loginWidgetPanelStyle")

    methodend

    method public void setLogo(BBjString logo$)
        #logoURL$ = logo$
    methodend

    method public Boolean doModal()

        if #AllowRemember! then
            token$ = BBjAPI().getThinClient().getUserProperty(0,"remember",err=*next)
            if token$>"" and #authProvider!.checkToken(token$) then
                token$ = #authProvider!.getToken()
                BBjAPI().getThinClient().setUserProperty(0,"Strict","remember",token$)
                #User$ = #authProvider!.getUser()
                methodret Boolean.TRUE
            fi
        fi  
        
        declare BBjImageCtrl logo!
        declare BBjButton loginBtn!
        declare BBjCheckBox rememberMe!
        declare BBjChildWindow linkWrapper!
        declare BBjStaticText forgotPw!
        declare BBjChildWindow content!

        content! = #loginWrapper!.addChildWindow(#loginWrapper!.getAvailableControlID(),0,0,0,0,"",$00100800$,BBjAPI().getSysGui().getAvailableContext())
        content!.setDockStyle("height","calc(531px - 112px)")
        content!.setDockStyle("padding","56px 44px")
        content!.addPanelStyle("loginWidgetContentPanelStyle")
        
        logo! = content!.addImageCtrl(content!.getAvailableControlID(),0,0,0,0, #logoURL$)
        logo!.setStyle("height","fit-content")
        logo!.setStyle("width","fit-content")
        
        #username! = new InputField(content!,content!.getAvailableControlID(),"WebKit/framework/LoginDialog/assets/user.png")
        #username!.setCallback(InputField.ON_SUFFIX_PRESSED,#this!,"onUserIconPressed")
        #username!.setLabel("Username")
        #username!.setPlaceHolder("name@company.com")
        #username!.setDockStyle("margin-top","50px")
        
        #password! = new InputField(content!,content!.getAvailableControlID(),"WebKit/framework/LoginDialog/assets/eye.png",1)
        #password!.setCallback(InputField.ON_SUFFIX_PRESSED,#this!,"onShowPasswordIconPressed")
        #password!.setLabel("Password")
        #password!.setPlaceHolder("Your Password")
        #password!.setDockStyle("margin-top","28px")
        
        rememberMe! = content!.addCheckBox(content!.getAvailableControlID(),0,0,0,0,"Keep me signed in")
        rememberMe!.setStyle("margin-top","32px")
        rememberMe!.addStyle("loginWidgetCheckBoxStyle")
        rememberMe!.addStyle("loginWidgetCheckBoxLabel")
        rememberMe!.addStyle("loginWidgetCheckBoxInputWrapper")
        rememberMe!.addStyle("loginWidgetCheckBoxControl")
        REM TODO use theming or ask sebatian how he did this in the p360
        REM         rememberMe!.addStyle("loginWidgetCheckBoxIcon")
        
        st! = content!.addStaticText(99,0,0,0,0,"<html>Check your input</html>",$0010$)
        st!.setStyle("color","red")
        st!.setStyle("margin-top", "5px")

        loginBtn! = content!.addButton(content!.getAvailableControlID(),0,0,0,0,"Login")
        loginBtn!.setCallback(BBjAPI().ON_BUTTON_PUSH,"login")
        loginBtn!.setStyle("margin-top","40px")
        loginBtn!.addStyle("loginWidgetBtnStyle")
        loginBtn!.addStyle("loginWidgetBtnLabel")
        loginBtn!.addStyle("loginWidgetBtnControl")
        
        linkWrapper! = content!.addChildWindow(content!.getAvailableControlID(),0,0,0,0,"",$00100800$,BBjAPI().getSysGui().getAvailableContext())
        linkWrapper!.setCallback(BBjAPI.ON_MOUSE_DOWN,#this!,"forgotPassword")
        linkWrapper!.setDockStyle("margin-top","16px")
        linkWrapper!.setDockStyle("justify-self","center")
        linkWrapper!.setDockStyle("height","fit-content")
        linkWrapper!.setDockStyle("width","fit-content")
        
        forgotPW! = linkWrapper!.addStaticText(linkWrapper!.getAvailableControlID(),0,0,0,0,"Forgot your password?")
        forgotPW!.addStyle("loginWidgetForgotPWStlye")

        process_events

        login:
            user$=#username!.getText(0)
            pass$=#password!.getText(1)
                
                if #authProvider!.checkLogin(user$,pass$) then
                    #User$ = user$
                    if rememberMe! <> null() AND rememberMe!.isSelected() then
                        token$ = #authProvider!.getToken()
                        BBjAPI().getThinClient().setUserProperty(0,"Strict","remember",token$)
                    fi
                    
                    #loginWrapper!.destroy()
                    methodret Boolean.TRUE
                else
                content!.getControl(99).setVisible(1)
                fi
                return
           
           onGoogleLogin:
           onMicrosoftLogin:
                ev! = BBjAPI().getLastEvent()
                profile! = ev!.getObject()
                if #authProvider!.checkLoginWithAccountProfile(profile!) then
                    #User$ = str(profile!.getFullName())
                    #Profile! = CAST(AccountProfile,profile!)
                    #window!.destroy()
                    methodret Boolean.TRUE
                fi
                return

    methodend

    method public void onUserIconPressed(BBjCustomEvent event!)
        a = msgbox("UserIcon Pressed")
    methodend

    method public void onShowPasswordIconPressed(BBjCustomEvent event!)

        if #password!.isPasswordVisible() then 
            #password!.setPasswordVisible(0)
            #password!.setSuffixIcon("WebKit/framework/LoginDialog/assets/eye.png")
        else
            #password!.setPasswordVisible(1)
            #password!.setSuffixIcon("WebKit/framework/LoginDialog/assets/eye-off.png")
        endif    
    methodend

    method public void onLoginBtnPressed(BBjButtonPushEvent event!)
        user$=#username!.getText(0)
        pass$=#password!.getText(1)

        #fireEvent(#ON_LOGIN,"")
    methodend

    method public void forgotPassword(BBjMouseDownEvent event!)
        a = msgbox("Forgot Password")
    methodend

    method public AccountProfile getProfile()
        methodret #Profile!
    methodend
    
    method public static void clearRememberToken()
        BBjAPI().getThinClient().setUserProperty(0,"Strict","remember",token$)
    methodend

classend

token$ = BBjAPI().getThinClient().getUserProperty(0,"remember",err=*next)
if token$>"" then 
    if msgbox("Clear Remember Token??",36,"Token") = 6 then
        LoginDialog.clearRememberToken()
    fi
fi

l! = new LoginDialog(new SampleAuthProvider())
l!.setAllowRemember(Boolean.TRUE)

if l!.doModal() then
    a=msgbox(l!.getUser(),0,"SUCCESS")
fi

