use ::WebKit/widgets/BBjWebWidget.bbj::BBjWebWidget

use ::WebKit/framework/DrawerCloseIcon.bbj::DrawerCloseIcon

class public DrawerHeader extends BBjWebWidget 
    
    field private BBjChildWindow window!
    
    field private BBjString logo!
    
    field private BBjString smallLogo!
    
    field public static BBjNumber ON_DRAWER_CLOSE_ICON_PUSH = DrawerCloseIcon.getON_DRAWER_CLOSE_ICON_PUSH()
    
    method public DrawerHeader(BBjWindow parent!, BBjString logo!, BBjString smallLogo!)
        #window! = parent!.addChildWindow(parent!.getAvailableControlID(),0,0,0,0,"",$00100800$,BBjAPI().getSysGui().getAvailableContext())
        #window!.addPanelStyle("drawerHeaderPanelStyle")
        #window!.addPanelStyle("drawerHeaderPanelStyleOpen")
        #window!.addPanelStyle("drawerHeaderPanelStyleClosed")
        #setCanvas(#window!)
        #logo!= logo!
        #smallLogo! = smallLogo!
        #redraw(1)
    methodend
    
    method public void redraw(Boolean init!)
        declare BBjImageCtrl logo!
        declare BBjImageCtrl smallLogo!
        declare DrawerCloseIcon iconBtn!
        if init!
            REM TODO
            logo! = #window!.addImageCtrl(#window!.getAvailableControlID(),0,0,0,0,#logo!)
            REM if set to fit-content it jumps from 64px to 66 when clicking on the dashboard
            logo!.setStyle("height","64px")
            logo!.setStyle("width","fit-content")
            logo!.setStyle("grid-row-start","1")
            logo!.setStyle("grid-column-start","1")
            logo!.addStyle("drawerHeaderLogo")
            
            smallLogo! = #window!.addImageCtrl(#window!.getAvailableControlID(),0,0,0,0,#smallLogo!)
            smallLogo!.setStyle("display","none")
            smallLogo!.addStyle("drawerHeaderLogoSmallClosed")
            smallLogo!.addStyle("drawerHeaderLogoSmallOpen")
            
            iconBtn! = new DrawerCloseIcon(#window!)
            iconBtn!.setCallback(DrawerCloseIcon.ON_DRAWER_CLOSE_ICON_PUSH,#this!,"onDrawerIconPush")
        endif    
    methodend
    
    method public void onDrawerIconPush(BBjCustomEvent event!)
        #fireEvent(#ON_DRAWER_CLOSE_ICON_PUSH,"")
    methodend
classend