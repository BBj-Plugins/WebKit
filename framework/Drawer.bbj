use ::WebKit/widgets/BBjWebWidget.bbj::BBjWebWidget

use ::WebKit/framework/DrawerHeader.bbj::DrawerHeader
use ::WebKit/framework/DashboardWidget.bbj::DashboardWidget
use ::WebKit/framework/DrawerSearchWidget.bbj::DrawerSearchWidget
use ::WebKit/framework/DrawerMenuTile.bbj::DrawerMenuTile
use ::WebKit/framework/Divider.bbj::Divider
use ::WebKit/model/DrawerMenuTileEntry.bbj::DrawerMenuTileEntry
use ::WebKit/framework/DrawerFooter.bbj::DrawerFooter
use ::WebKit/framework/DrawerOpenIcon.bbj::DrawerOpenIcon

class public Drawer extends BBjWebWidget 
    
    field private BBjChildWindow window!
    
    field private Boolean open! = 0
    
    field private BBjVector tiles! = new BBjVector()
    
    field private BBjChildWindow tilesWrapper!
    
    field private BBjChildWindow optionalControl!
    
    method public Drawer(BBjWindow parent!, BBjVector tiles!)
        #window! = parent!.addChildWindow(parent!.getAvailableControlID(),0,0,0,0,"",$00100800$,BBjAPI().getSysGui().getAvailableContext())
        #window!.addPanelStyle("drawerPanelStyle")
REM         #window!.addPanelStyle("drawerOpen")
        #window!.addPanelStyle("drawerAnimationOpen")
        #window!.addPanelStyle("drawerAnimationClose")
        #tiles! = tiles!
        #setCanvas(#window!)
        #redraw(1)
    methodend
    
    method public void redraw(Boolean init!)
        declare DrawerHeader header!
        declare DashboardWidget dashboard!
        declare DrawerSearchWidget searchWidget!
        declare Divider topDivider!
        declare Divider bottomDivider!
        declare BBjChildWindow tilesWrapper!
        declare DrawerFooter footer!
        declare BBjChildWindow footerWrapper!
        declare BBjString templateRows!
        declare DrawerOpenIcon drawerOpenIcon!
       rem  "WebKit/demo/assets/prodin_logo.png"
       
       rem 12px drawertile space
       
       templateRows! = "40px"
       
        if init! 
            #open! = 1
            header! = new DrawerHeader(#window!,"WebKit/demo/assets/prodin_logo.png","WebKit/demo/assets/prodin_logo_small.png")
            header!.setCallback(DrawerHeader.ON_DRAWER_CLOSE_ICON_PUSH,#this!,"closeDrawer")
            
            drawerOpenIcon! = new DrawerOpenIcon(#window!)
            drawerOpenIcon!.setCallback(DrawerOpenIcon.ON_DRAWER_OPEN_ICON_PUSH,#this!,"openDrawer")
            
            dashboard! = new DashboardWidget(#window!)
            searchWidget! = new DrawerSearchWidget(#window!)
            
            topDivider! = new Divider(#window!)
            topDivider!.setColor("#DDE0E1")
            
            #tilesWrapper! = #window!.addChildWindow(#window!.getAvailableControlID(),0,0,0,0,"",$00100800$,BBjAPI().getSysGui().getAvailableContext())
            #tilesWrapper!.setPanelStyle("display","grid")
            #tilesWrapper!.setPanelStyle("overflow","auto")
            #tilesWrapper!.setPanelStyle("height","calc(100vh - 381px)")
            
            bottomDivider! = new Divider(#window!)
            bottomDivider!.setColor("#DDE0E1")
            
            footerWrapper! = #window!.addChildWindow(#window!.getAvailableControlID(),0,0,0,0,"",$00100800$,BBjAPI().getSysGui().getAvailableContext())
            footerWrapper!.setPanelStyle("height","117px")
            footerWrapper!.setPanelStyle("display","grid")
            
            footer! = new DrawerFooter(footerWrapper!,#window! ,"David Williams","Art Director")
            footer!.setDockStyle("align-self","center")
REM             footer!.setDockStyle("justify-self","center")
            
            
            declare DrawerMenuTileEntry entry!
            declare BBjChildWindow spacer!
            
            for i = 0 to #tiles!.size() -1
                entry! = cast(DrawerMenuTileEntry,#tiles!.get(i))
                tile! = new DrawerMenuTile(#tilesWrapper!,entry!.getID(),entry!.getIconPath(),entry!.getTitle())
                templateRows! = templateRows! + " 48px" 
                if i <> #tiles!.size() -1 then 
                    templateRows! = templateRows! + " 12px"
                    spacer! = #tilesWrapper!.addChildWindow(#tilesWrapper!.getAvailableControlID(),0,0,0,0,"",$00100800$,BBjAPI().getSysGui().getAvailableContext())
                    spacer!.setDockStyle("height","12px")
                endif
            next i
REM             a = msgbox(templateRows!)
            #tilesWrapper!.setPanelStyle("grid-template-rows",templateRows!)
            
        endif
    methodend
    
    method public BBjChildWindow getOptionalControlAboveTiles()
        if #optionalControl! = null() then
            #optionalControl! = #tilesWrapper!.addChildWindow(#tilesWrapper!.getAvailableControlID(),0,0,0,0,"",$00100800$,BBjAPI().getSysGui().getAvailableContext())
        endif
        methodret #optionalControl!
    methodend
    
    method public void closeDrawer(BBjCustomEvent event!)
        #window!.addStyle("drawerClosed")
        #window!.removeStyle("drawerOpen")
        #open! = !#open!
    methodend
    
    method public void openDrawer(BBjCustomEvent event!)
        #window!.addStyle("drawerOpen")
        #window!.removeStyle("drawerClosed")
        #open! = !#open!
    methodend
    
    method public Boolean isOpen()
        methodret #open!
    methodend
classend