use ::WebKit/util/DynamicLoader.bbj::DynamicLoader
use ::WebKit/util/ClientUtil.bbj::ClientUtil
use ::WebKit/demo/ChileCompanyCustomerPortal/util/DbUtil.bbj::DbUtil
use ::WebKit/widgets/common/Overlay/Overlay.bbj::Overlay
use ::WebKit/widgets/InputField/InputField.bbj::InputField
use ::WebKit/widgets/common/Divider/Divider.bbj::Divider
use com.basiscomponents.db.ResultSet
use com.basiscomponents.db.DataRow

class public FormControlPanel
    field private BBjChildWindow window!

    field private BBjChildWindow modalWnd!

    field private DbUtil dbUtil!

    field private ResultSet RS!

    field private DataRow dataRow!

    field private Boolean isEdit! = 0

    field private BBjString entityName$

    field private Overlay overlay!

    method public FormControlPanel(BBjWindow parent!)
        DynamicLoader.addLocalCSS("WebKit/framework/FormControlPanel/FormControlPanel.css")
        #window! = parent!.addChildWindow(parent!.getAvailableControlID(),0,0,0,0,"",$00100800$,BBjAPI().getSysGui().getAvailableContext())

        #dbUtil! = new DbUtil()
    methodend

    method public void setDataRow(DataRow dataRow!)
        #dataRow! = dataRow!
    methodend

    method public DataRow getDataRow()
        methodret #dataRow!
    methodend

    method public void setResultSet(ResultSet rs!)
        #RS! = rs!
    methodend

    method public DataRow getResultSet()
        methodret #RS!
    methodend

    method public void setEntityName(BBjString entityName$)
        #entityName$ = entityName$
    methodend

    method public void setIsEdit(Boolean isEdit!)
        #isEdit! = isEdit!
    methodend

    method public void drawForm(BBjChildWindow modalWnd!)
       columnNames! = #RS!.getColumnNames()
       columnCount! = #RS!.getColumnCount()

       topWnd! = modalWnd!.addChildWindow(modalWnd!.getAvailableControlID(),0,0,0,0,"",$00100800$,BBjAPI().getSysGui().getAvailableContext())
       topWnd!.addStyle("topWndPanel")

       modalTitle$ = ""
       if #isEdit! = 0 THEN
           modalTitle$ = modalTitle$ + "Create " + #entityName$
       else
           modalTitle$ = modalTitle$ + "Edit " + #entityName$
       fi
       modalHeader! = topWnd!.addStaticText(topWnd!.getAvailableControlID(),0,0,0,0, modalTitle$)
       modalHeader!.addStyle("modalHeader")

       topDivider! = new Divider(modalWnd!)
       topDivider!.setColor("#DDE0E1")


       formWnd! = modalWnd!.addChildWindow(modalWnd!.getAvailableControlID(),0,0,0,0,"",$00100800$,BBjAPI().getSysGui().getAvailableContext())
       formWnd!.addPanelStyle("formPanel")

       if #isEdit! = 1 THEN
          dataRows! = #RS!.getDataRows()

          #dataRow! = CAST(DataRow, dataRows!.get(0))
       fi

       for i = 0 to columnNames!.size() - 1
           fieldWnd! = formWnd!.addChildWindow(formWnd!.getAvailableControlID(),0,0,0,0,"",$00100800$,BBjAPI().getSysGui().getAvailableContext())


           if i < (columnCount! / 2) THEN
            fieldWnd!.setStyle("grid-column", "1")
            fieldWnd!.setStyle("grid-row", str(i + 1))
           else 
            fieldWnd!.setStyle("grid-row", str(i - (columnCount! / 2)))
            fieldWnd!.setStyle("grid-column", "2")
           fi
           fieldWnd!.addStyle("edit-field")

           columnName! = columnNames!.get(i)
           _fieldLabel! = new InputField(fieldWnd!)

           if #isEdit! = 1 THEN
             _fieldLabel!.setText(str(#dataRow!.getFieldValue(columnName!)))
           else
             _fieldLabel!.setPlaceHolder(columnName!)
           fi
           _fieldLabel!.setLabel(columnName!)


        NEXT i
        bottomDivider! = new Divider(modalWnd!)
        bottomDivider!.setColor("#DDE0E1")

        footerWnd! = modalWnd!.addChildWindow(modalWnd!.getAvailableControlID(),0,0,0,0,"",$00100800$,BBjAPI().getSysGui().getAvailableContext())
        footerWnd!.addStyle("footerWnd")

        cancelBtn! = footerWnd!.addButton(2,0,0,0,0,"Cancel")
        cancelBtn!.setCallback(BBjAPI().ON_BUTTON_PUSH,#this!, "onFormCancel")
        cancelBtn!.addStyle("createBtn")
        REM cancelBtn!.setStyle("margin-left", "auto")
        cancelBtn!.setAttribute("theme", "gray")

        createBtn! = footerWnd!.addButton(1,0,0,0,0,"Submit")
        createBtn!.setCallback(BBjAPI().ON_BUTTON_PUSH,#this!, "onFormSubmit")
        createBtn!.addStyle("createBtn")
        REM createBtn!.setStyle("margin-left", "auto")
        createBtn!.setAttribute("theme", "success")

    methodend


    method public void show()
        #modalWnd! = #window!.addChildWindow(#window!.getAvailableControlID(),0,0,0,0,"",$00100800$,BBjAPI().getSysGui().getAvailableContext())
        #modalWnd!.addPanelStyle("formControlPanel")
        #drawForm(#modalWnd!)
        
        #overlay! = new Overlay(#window!, #modalWnd!)
        #overlay!.setCallback(Overlay.ON_OVERLAY_DISSMISSED, #this!,"onOverlayDissmiss")
    methodend

    method public void onFormSubmit(BBjButtonPushEvent ev!)
        #overlay!.onOverlayDissmiss(null())
        BBjAPI().postCustomEvent("ON_FORM_SUBMIT", #dataRow!)
    methodend

    method public void onFormCancel(BBjButtonPushEvent ev!)
        #overlay!.onOverlayDissmiss(null())
    methodend

    method public void onOverlayDissmiss(BBjCustomEvent ev!)
       #modalWnd!.destroy() 
    methodend
classend
