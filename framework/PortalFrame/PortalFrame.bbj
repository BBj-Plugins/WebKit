use ::WebKit/widgets/IconTile.bbj::IconTile
use ::WebKit/model/Menu.bbj::Menu
use ::WebKit/model/Menu.bbj::MenuItem
use ::WebKit/framework/MenuPanel/MenuPanel.bbj::MenuPanel
use ::WebKit/framework/EmbedPanel/EmbedPanel.bbj::EmbedPanel
use ::BBjWidget/BBjWidget.bbj::BBjWidget
use ::WebKit/framework/LoginDialog.bbj::LoginDialog
use ::WebKit/util/ClientUtil.bbj::ClientUtil
use ::AuthKit/profile/AccountProfile.bbj::AccountProfile

use java.util.HashMap

class public PortalFrame

    field private BBjTopLevelWindow window!
    field private BBjChildWindow Drawer!
    field private BBjChildWindow Main!
    field private BBjChildWindow AccountDisplay!
    field private BBjChildWindow Avatar!
    
    field public BBjString Username!
    field public BBjString Title!
    field public Menu Menu!
    field public AccountProfile Profile!
    
    field private Boolean drawerOpen! = Boolean.FALSE
    field private HashMap win_list! = new HashMap()
    field private BBjWidget curr_win! 
    field private MenuPanel menuPanel!

    field private BBjPopupMenu AccountMenu!
    
    method public Boolean doModal()

            sysgui! =BBjAPI().openSysGui("X0")
            
            #window! = CAST(BBjTopLevelWindow, sysgui!.addWindow(sysgui!.getAvailableContext(),25,25,1200,750,#Title!,$01101083$))
            
            rem add one invisible button to force the BASIS variables to be loaded for CSS            
            #window!.addButton(1000,0,0,0,0,"TEST",$0010$ )
            
            cw_top! = #window!.addChildWindow(#window!.getAvailableControlID(),0,0,100,80,"",$00100800$,sysgui!.getAvailableContext())
            cw_top!.setNoEdge(1)
            cw_top!.addStyle("topbar")
            
            menu_btn! = cw_top!.addChildWindow(#window!.getAvailableControlID(),0,0,100,80,"",$00100800$,sysgui!.getAvailableContext())
            menu_btn!.addStyle("menutoggle")
            
            IconCtrl! = menu_btn!.addStaticText(100,0,0,200,22,"menu")
            IconCtrl!.addStyle("material-icons")
            IconCtrl!.addStyle("tilelabel")
            menu_btn!.setCallback(BBjAPI.ON_MOUSE_DOWN,#this!,"onToggleMenu")
            
            
            logo! = cw_top!.addChildWindow(cw_top!.getAvailableControlID(),0,0,200,80,"",$00100800$,sysgui!.getAvailableContext())
            logo!.addPanelStyle("logo")            

            #Drawer! = #window!.addChildWindow(#window!.getAvailableControlID(),0,0,100,80,"",$00100800$,sysgui!.getAvailableContext())
            #Drawer!.addStyle("drawermenu")
            #Drawer!.addStyle("drawerclosed")
            
            main! = #window!.addChildWindow(#window!.getAvailableControlID(),0,0,100,80,"",$00100800$,sysgui!.getAvailableContext())
            main!.addStyle("mainarea")
            #Main! = cast(BBjChildWindow,main!)
             
            menuitems! = #Menu!.getChildren(#Menu!.getRoot()) 
            it! = menuitems!.iterator()
            while it!.hasNext()
                declare auto MenuItem menuitem!
                menuitem! = it!.next()
                id% = menuitem!.getNodeId()
                btn! = new IconTile(#Drawer!,id%)
                btn!.setText(menuitem!.getCaption())
                btn!.setIcon(menuitem!.getIcon())
                btn!.setToolTip(menuitem!.getToolTip())
                btn!.setCallback(BBjAPI.ON_BUTTON_PUSH,#this!,"onMenuItemClick")
            wend

            #AccountDisplay! = CAST(BBjChildWindow,cw_top!.addChildWindow(cw_top!.getAvailableControlID(),0,0,100,80,"",$00100800$,sysgui!.getAvailableContext()))
            #AccountDisplay!.setCallback(BBjAPI.ON_POPUP_REQUEST,#this!,"onAvatarAreaClick")
            
            account_name! = #AccountDisplay!.addStaticText(1221,0,0,200,80,#Username!)
            account_name!.addStyle("account")
            
            #Avatar! = #AccountDisplay!.addChildWindow(100,0,0,100,80,"",$00100800$,sysgui!.getAvailableContext())
            #Avatar!.addStyle("avatar")
            
            #AccountMenu! = BBjAPI().getSysGui().addPopupMenu()
            menu_logout!=#AccountMenu!.addMenuItem(-32027,"Logout")
            menu_logout!.setCallback(BBjAPI.ON_POPUP_ITEM_SELECT,"onAccountLogout")
            
            #AccountDisplay!.setCallback(BBjAPI.ON_MOUSE_DOWN,#this!,"onAvatarAreaClick")
            
            if #Profile! <> null() then
                #Avatar!.setDockStyle("background-image","url("+#Profile!.getPictureUrl()+")")
            fi
            
            BBjAPI().setCustomEventCallback("logout","logout")
            
            
            anchor$ = ClientUtil.getUrlAnchor()
            if anchor$>"" then
                #navigateTo(anchor$)
            fi

                    
            process_events
            
            logout:
                methodret 1
            
    methodend
    
    method public void navigateTo(BBjString anchor$)
        item! = #Menu!.getItem(anchor$)
        if item! <> null() then
            #navigateTo(item!)
        fi
   methodend

    method public void onMenuItemClick(BBjCustomEvent ev!)
        nodeId=num(str(ev!.getObject()))
        item!=#Menu!.getItem(nodeId)
        #navigateTo(item!)
   methodend

   method public void navigateTo(MenuItem item!)
        if #curr_win! <> null() and !#curr_win!.isDestroyed() then
            #curr_win!.setVisible(0)
            #curr_win!=null()
        fi
                
        if item!.hasChildren() then
            if (#menuPanel! = null()) then
                #menuPanel! = new MenuPanel(#Main!)
                #menuPanel!.setMenu(#Menu!)
                #menuPanel!.setCallback(BBjAPI.ON_BUTTON_PUSH,#this!,"onMenuItemClick")
            fi
            #menuPanel!.setParentNode(item!.getNodeId())
            #menuPanel!.setVisible(1)
            #curr_win! = #menuPanel! 
        else
        
            if #win_list!.containsKey(item!.getNodeId()) then
                #curr_win!= CAST(BBjWidget,#win_list!.get(item!.getNodeId()))
                #curr_win!.setVisible(1)
            else
                pgm$ = item!.getProgram()
                if pgm$>"" then
                    starttype = item!.getStartType()
                    
                    switch starttype
                        case 0
                            x! = null()
                            x! = eval("new "+pgm$+"(#Main!)",err=*next)
                            if x! = null() then
                                a = msgbox("Error starting "+item!.getCaption(),0,"ERROR")
                            else 
                                #curr_win! = CAST(BBjWidget,x!)
                            fi
                            break
                            
                        case 1
                        case 2
                            x! = new EmbedPanel(#Main!)
                            x!.start(item!)
                            #curr_win! = CAST(BBjWidget,x!)
                            x!.setCallback(BBjAPI().ON_CLOSE,#this!,"onEmbedPanelClose")
                            break
                        case default
                            rem ignore
                            break
                    swend
                    if #curr_win! <> null() then
                        #win_list!.put(item!.getNodeId(),#curr_win!)
                    fi
                    
                fi
            fi
        fi
        ClientUtil.setUrlAnchor(item!.getCaption(),item!.getCaption())
    methodend
    
    method public void onAvatarAreaClick(BBjEvent ev!)
        #AccountMenu!.show(#Avatar!,5,50)
    methodend
    
    method public void onAccountLogout (BBjEvent ev!)
        a=msgbox("logout")
        LoginDialog.clearRememberToken()
        BBjAPI().postCustomEvent("logout","")
    methodend
                
    method public void onEmbedPanelClose(BBjCustomEvent ev!)
        item! = ev!.getObject()
        nodeId = item!.getNodeId()
        if #win_list!.containsKey(nodeId) then
            #win_list!.remove(nodeId)
        fi
         
    methodend

    method public void onToggleMenu(BBjEvent ev!)
    rem drawer_status 0=open, 1=closed
    if #drawerOpen! then
         #drawerOpen!=Boolean.FALSE
         #Drawer!.addStyle("drawerclosed")
         #Drawer!.removeStyle("draweropen")
    else
         #drawerOpen!=Boolean.TRUE
         #Drawer!.addStyle("draweropen")
         #Drawer!.removeStyle("drawerclosed")
    fi
    methodend
return

classend



