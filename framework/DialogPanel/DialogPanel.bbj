use ::BBjWidget/BBjWidget.bbj::BBjWidget
use ::WebKit/util/Icons.bbj::Icons
use ::WebKit/util/DynamicLoader.bbj::DynamicLoader
use ::WebKit/widgets/common/Divider/Divider.bbj::Divider

use ::WebKit/i18n/I18n.bbj::I18n

class public DialogPanel extends BBjWidget
    field private BBjChildWindow dialogWnd!    
    field private BBjWindow parent!    

    REM dialog header related
    field private BBjChildWindow header!
    field private BBjChildWindow closeIconWnd!
    field private Boolean fShowCloseIcon! = 1
    field private Boolean closeByOutsideClick! = 0

    REM dialog body related
    field private BBjChildWindow body!
    
    REM dialog footer related
    field private BBjChildWindow footer!
    field private BBjButton submitBtn!
    field private BBjButton cancelBtn!

    field private BBjString submitBtnText$ = I18n.getTranslation("submit")
    field private BBjString submitBtnTheme$ = "primary"
    field private BBjString submitBtnCustomCssClass$ = "footerBtn"
    field public static BBjNumber ON_SUBMIT = 1234

    field private BBjString cancelBtnText$ = "Cancel"
    field private BBjString cancelBtnTheme$ = "default"
    field private BBjString cancelBtnCustomCssClass$ = "footerBtn"
    field public static BBjNumber ON_CLOSE = BBjAPI.ON_CLOSE
    
    field private BBjStaticText lblTitle!


    method public DialogPanel(BBjWindow canvas!)
        DynamicLoader.addLocalCSS("WebKit/framework/DialogPanel/DialogPanel.css")

        #parent! = canvas!.addChildWindow(canvas!.getAvailableControlID(),0,0,0,0,"",$00108810$,BBjAPI().getSysGui().getAvailableContext())
        #parent!.addStyle("ChildBackground")
        #parent!.setCallback(BBjAPI.ON_MOUSE_DOWN,#this!,"onOutsideClick")
        
        #dialogWnd! = canvas!.addChildWindow(canvas!.getAvailableControlID(),0,0,0,0,"",$00108810$,BBjAPI().getSysGui().getAvailableContext())
        
        #setText("Dialog")
        
        #createParts()
        
    methodend

    method public DialogPanel()
        DynamicLoader.addLocalCSS("WebKit/framework/DialogPanel/DialogPanel.css")

        sysgui! =BBjAPI().openSysGui("X0")
        declare BBjTopLevelWindow window!
        window! = CAST(BBjTopLevelWindow,sysgui!.addWindow(sysgui!.getAvailableContext(),25,25,1200,750,"",$01101093$))
        window!.setCallback(BBjAPI.ON_MOUSE_DOWN,#this!,"onOutsideClick")
        
        window!.addStyle("dialogWnd")
        #dialogWnd! = window!.addChildWindow(window!.getAvailableControlID(),0,0,0,0,"",$00108810$,BBjAPI().getSysGui().getAvailableContext())

        #parent! = window!
        
        #setText("Dialog")
        
        #createParts()
        
    methodend

    method public void renderTopBar()
       #header! = #dialogWnd!.addChildWindow(#dialogWnd!.getAvailableControlID(),0,0,0,0,"",$00108800$,BBjAPI().getSysGui().getAvailableContext())
       #header!.addStyle("headerWnd")
       #header!.addPanelStyle("headerPanel")

       #lblTitle! = #header!.addStaticText(#header!.getAvailableControlID(),0,0,0,0, #getText())
       #lblTitle!.addStyle("headerTitle")
       
       #closeIconWnd! = #header!.addChildWindow(#header!.getAvailableControlID(), 0,0,0,0,"",$00100800$,BBjAPI().getSysGui().getAvailableContext())
       #closeIconWnd!.addStyle("closeIconWnd")
       iconpool$=stbl("icon-pool", err=*next)
       ico$="x"
       if iconpool$="fa" then ico$="fas-window-close"
       CloseIcon! = Icons.getIconOnWindow(#closeIconWnd!, ico$)
       CloseIcon!.addStyle("closeIcon")
       #closeIconWnd!.setCallback(BBjAPI().ON_MOUSE_DOWN,#this!, "onClose")
       
       topDivider! = new Divider(#dialogWnd!)
       topDivider!.setColor("var(--bbj-divider-color, #DDE0E1)")
        
    methodend

    method public void renderFooter()
        bottomDivider! = new Divider(#dialogWnd!)
        bottomDivider!.setColor("var(--bbj-divider-color, #DDE0E1)")

        #footer! = #dialogWnd!.addChildWindow(#dialogWnd!.getAvailableControlID(),0,0,0,0,"",$00108800$,BBjAPI().getSysGui().getAvailableContext())
        #footer!.addStyle("footerWnd")

        #cancelBtn! = #footer!.addButton(2,0,0,0,0, #cancelBtnText$)
        #cancelBtn!.setCallback(BBjAPI().ON_BUTTON_PUSH,#this!, "onCancel")
        #cancelBtn!.addStyle(#cancelBtnCustomCssClass$)
        #cancelBtn!.setAttribute("theme", #cancelBtnTheme$)

        #submitBtn! = #footer!.addButton(1,0,0,0,0,#submitBtnText$)
        #submitBtn!.setCallback(BBjAPI().ON_BUTTON_PUSH,#this!, "onSubmit")
        #submitBtn!.addStyle(#submitBtnCustomCssClass$)
        #submitBtn!.setAttribute("theme", #submitBtnTheme$)
    methodend

    method public void onSubmit(BBjButtonPushEvent ev!)
        #dialogWnd!.destroy()
        #parent!.destroy()
        #fireEvent(ON_SUBMIT, "")
    methodend

    method public void onCancel(BBjButtonPushEvent ev!)
        #doCancel()
    methodend

    method public void onClose(BBjMouseDownEvent ev!)
        #doCancel()
    methodend

    method public void initializeBody()
       #body! = #dialogWnd!.addChildWindow(#dialogWnd!.getAvailableControlID(),0,0,0,0,"",$00108800$,BBjAPI().getSysGui().getAvailableContext())
       #body!.addPanelStyle("bodyPanel")
    methodend
    
    method public void doCancel()
        #dialogWnd!.destroy()
        #parent!.destroy()
        #fireEvent(ON_CLOSE, "")    
    methodend

    method public void onOutsideClick(BBjEvent ev!)
        if #closeByOutsideClick! then
            #doCancel()
        fi
    methodend

    method public void createParts()
        #dialogWnd!.addPanelStyle("dialogPanel")
        #renderTopBar()
        #initializeBody()
        #renderFooter()
    methodend
    
    method public void setShowCloseIcon(Boolean f_show!)
        #fShowCloseIcon!=f_show!
        #closeIconWnd!.setVisible(f_show!)
    methodend

    method public void setSubmitBtnText(BBjString text!)
        #submitBtnText$ = text!
        #submitBtn!.setText(text!)
    methodend
    
    method public void setSubmitBtnTheme(BBjString text!)
        #submitBtnTheme$ = text!
        #submitBtn!.setAttribute("theme",text!)
    methodend
    
    method public void setSubmitBtnCustomCssClass(BBjString text!)
        #submitBtnCustomCssClass$ = text!
        #submitBtn!.addStyle(text!)
    methodend
    
    method public void setCancelBtnText(BBjString text!)
        #cancelBtnText$ = text!
        #cancelBtn!.setText(text!)
    methodend
    
    method public void setCancelBtnTheme(BBjString text!)
        #cancelBtnText$ = text!
        #cancelBtn!.setAttribute("theme",text!)
    methodend
    
    method public void setCancelBtnCustomCssClass(BBjString text!)
        #cancelBtnText$ = text!
        #cancelBtn!.addStyle(text!)
    methodend
    
    method public void setCloseByOutsideClick(Boolean f_outsideclick!)
        #closeByOutsideClick! = f_outsideclick!
    methodend    

    method public BBjChildWindow getBody()
        methodret #body!
    methodend
    
    method public void show()
        #parent!.setVisible(Boolean.TRUE)
        #dialogWnd!.setVisible(Boolean.TRUE)
    methodend
    
    method public void setSubmitText(BBjString text!)
        #submitBtnText$ = text!
        #submitBtn!.setText(#submitBtnText$)
    methodend

    method public void setCancelText(BBjString text!)
        #cancelBtnText$ = text!
        #cancelBtn!.setText(#cancelBtnText$)
    methodend
    
    rem @Override
    method public void setText(String text!)
        #super!.setText(text!)
        if #lblTitle! <> null() then
            #lblTitle!.setText(text!)
        fi
    methodend
    
    
    
classend
