use ::WebKit/widgets/BBjWebWidget.bbj::BBjWebWidget
use ::WebKit/widgets/IconTile.bbj::IconTile

use ::WebKit/model/Menu.bbj::Menu
use ::WebKit/model/Menu.bbj::MenuItem
use java.util.HashMap


class public MenuPanel extends BBjWebWidget

    field public  Menu Menu!
    field private BBjNumber parentNodeID
    field private HashMap MenuItemTiles! = new HashMap()

    field private BBjStaticText Headline!
    field private BBjChildWindow MenuTilesBox!
    

    method public MenuPanel(BBjWindow wnd!)
        #super!.create(wnd!,wnd!.getAvailableControlID())
        #setVisible(0)
        wnd! = #getCanvas()
        wnd!.addPanelStyle("menupanel")
        #Headline! = wnd!.addStaticText(100,0,0,0,0,"")
        #Headline!.addStyle("tilesheadline")
        #MenuTilesBox! = #getCanvas().addChildWindow(wnd!.getAvailableControlID(),0,0,100,80,"",$00100800$,BBjAPI().getSysGui().getAvailableContext())
        #MenuTilesBox!.setPanelStyle("display","flex")
    methodend

    method private void cleanup()
        if #MenuItemTiles!.size()>0 then
            it! = #MenuItemTiles!.keySet().iterator()
            while it!.hasNext()
                t! = #MenuItemTiles!.get(it!.next())
                t!.destroy()
            wend
            #MenuItemTiles!.clear()
        fi
    methodend

    method public void setParentNode(BBjNumber nodeId)
        #cleanup()
        #parentNodeID = nodeId
        declare MenuItem node!
        declare MenuItem item!
        node! = #Menu!.getItem(nodeId)
        #Headline!.setText(node!.getCaption())
        
        v! = node!.getChildren()
        it! = v!.iterator()
        while it!.hasNext()
            item! = cast(MenuItem,it!.next())
             id%  = item!.getNodeId()
            tile! = new IconTile(#MenuTilesBox!, id%)
            tile!.setText(item!.getCaption())
            tile!.setIcon(item!.getIcon())
            tile!.setToolTip(item!.getToolTip())
            tile!.addPanelStyle("submenu")
            tile!.setCallback(BBjAPI.ON_BUTTON_PUSH,#this!,"onMenuItemClick")
            #MenuItemTiles!.put(item!.getNodeId(),tile!)
        wend
    methodend

    method public void onMenuItemClick(BBjCustomEvent ev!)
        nodeId=num(str(ev!.getObject()))
        item!=#Menu!.getItem(nodeId)
        if item!.hasChildren() then
            #setParentNode(nodeId)
        else
            a=msgbox(item!.getCaption(),0,"Clicked on panel entry")
        fi
    methodend    
classend