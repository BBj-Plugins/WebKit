use ::WebKit/widgets/IconTile.bbj::IconTile
use ::WebKit/model/Menu.bbj::Menu
use ::WebKit/model/Menu.bbj::MenuItem
use ::WebKit/framework/MenuPanel.bbj::MenuPanel
use ::WebKit/framework/EmbedPanel.bbj::EmbedPanel
use ::WebKit/widgets/BBjWebWidget.bbj::BBjWebWidget

class public PortalFrame

    field private BBjTopLevelWindow window!
    field private BBjChildWindow Drawer!
    field private BBjChildWindow Main!
    
    field public BBjString Username!
    field public Menu Menu!
    
    field private Boolean drawerOpen! = Boolean.FALSE
    field private java.util.HashMap win_list! = new java.util.HashMap()
    field private BBjWebWidget curr_win! 
    field private MenuPanel menuPanel!
    
    
    
    method public Boolean doModal()

            sysgui! =BBjAPI().openSysGui("X0")
            
            #window! = CAST(BBjTopLevelWindow, sysgui!.addWindow(sysgui!.getAvailableContext(),25,25,1200,750,"Customer Portal",$01101083$))

            cw_top! = #window!.addChildWindow(100,0,0,100,80,"",$00100800$,sysgui!.getAvailableContext())
            cw_top!.setNoEdge(1)
            cw_top!.addStyle("topbar")
            
            menu_btn! = cw_top!.addChildWindow(99,0,0,100,80,"",$00100800$,sysgui!.getAvailableContext())
            menu_btn!.addStyle("menutoggle")
            
            IconCtrl! = menu_btn!.addStaticText(100,0,0,200,22,"menu")
            IconCtrl!.addStyle("material-icons")
            IconCtrl!.addStyle("tilelabel")
            menu_btn!.setCallback(BBjAPI.ON_MOUSE_DOWN,#this!,"onToggleMenu")
            
            
            logo! = cw_top!.addChildWindow(101,0,0,200,80,"",$00100800$,sysgui!.getAvailableContext())
            logo!.addPanelStyle("logo")            

            #Drawer! = #window!.addChildWindow(101,0,0,100,80,"",$00100800$,sysgui!.getAvailableContext())
            #Drawer!.addStyle("drawermenu")
            #Drawer!.addStyle("drawerclosed")
            
            main! = #window!.addChildWindow(132,0,0,100,80,"",$00100800$,sysgui!.getAvailableContext())
            main!.addStyle("mainarea")
            #Main! = cast(BBjChildWindow,main!)
             
            menuitems! = #Menu!.getChildren(#Menu!.getRoot()) 
            it! = menuitems!.iterator()
            while it!.hasNext()
                declare auto MenuItem menuitem!
                menuitem! = it!.next()
                id% = menuitem!.getNodeId()
                btn! = new IconTile(#Drawer!,id%)
                btn!.setText(menuitem!.getCaption())
                btn!.setIcon(menuitem!.getIcon())
                btn!.setToolTip(menuitem!.getToolTip())
                btn!.setCallback(BBjAPI.ON_BUTTON_PUSH,#this!,"onMenuItemClick")
            wend

            account_name! = cw_top!.addStaticText(1221,0,0,200,80,#Username!)
            account_name!.addStyle("account")
            
            avatar! = cw_top!.addChildWindow(100,0,0,100,80,"",$00100800$,sysgui!.getAvailableContext())
            avatar!.addStyle("avatar")
            
            process_events
            
    methodend
    

    method public void onMenuItemClick(BBjCustomEvent ev!)
    
        if #curr_win! <> null() then
            #curr_win!.setVisible(0)
            #curr_win!=null()
        fi
    
        nodeId=num(str(ev!.getObject()))
        item!=#Menu!.getItem(nodeId)
        if item!.hasChildren() then
            if (#menuPanel! = null()) then
                #menuPanel! = new MenuPanel(#Main!)
                #menuPanel!.setMenu(#Menu!)
            fi
            #menuPanel!.setParentNode(nodeId)
            #menuPanel!.setVisible(1)
            #curr_win! = #menuPanel! 
        else
        
            if #win_list!.containsKey(nodeId) then
                #curr_win!= CAST(BBjWebWidget,#win_list!.get(nodeId))
                #curr_win!.setVisible(1)
            else
                pgm$ = item!.getProgram()
                if pgm$>"" then
                    starttype = item!.getStartType()
                    switch starttype
                        case 0
                            x! = eval("new "+pgm$+"(#Main!)")
                            #curr_win! = CAST(BBjWebWidget,x!)
                            break
                            
                        case 1
                            x! = new EmbedPanel(#Main!)
                            x!.loadBUI(item!.getCaption(),pgm$)
                            #curr_win! = CAST(BBjWebWidget,x!)
                            break
                        case 2
                            x! = new EmbedPanel(#Main!)
                            x!.loadDWC(item!.getCaption(),pgm$)
                            #curr_win! = CAST(BBjWebWidget,x!)
                            break
                        case default
                            rem ignore
                            break
                    swend
                    #win_list!.put(nodeId,#curr_win!)
                fi
            fi
        fi
    methodend

    method public void onToggleMenu(BBjEvent ev!)
    rem drawer_status 0=open, 1=closed
    if #drawerOpen! then
         #drawerOpen!=Boolean.FALSE
         #Drawer!.addStyle("drawerclosed")
         #Drawer!.removeStyle("draweropen")
    else
         #drawerOpen!=Boolean.TRUE
         #Drawer!.addStyle("draweropen")
         #Drawer!.removeStyle("drawerclosed")
    fi
    methodend
return

classend



