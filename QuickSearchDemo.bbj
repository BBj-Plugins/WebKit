use ::WebKit/util/ClientUtil.bbj::ClientUtil
use ::widgets/QuickSearch.bbj::QuickSearch
use com.basiscomponents.db.ResultSet
use com.basiscomponents.db.DataRow
use com.basiscomponents.db.DataField

if INFO(3,6)<>"6" then
    call "WebKit/util/deploy.bbj",pgm(-1)
FI

ClientUtil.init()
ClientUtil.addLocalCSS("WebKit/css/webkit.css")
ClientUtil.addStyleURL("https://fonts.googleapis.com/icon?family=Material+Icons")
ClientUtil.addLocalCSS("WebKit/demo/assets/demo.css")

declare BBjSysGui sysGui!

sysGui! = BBjAPI().openSysGui("X0")

declare BBjWindow mainWindow!

mainWindow! = sysGui!.addWindow(sysGui!.getAvailableContext(),25,25,1200,750,"QuickSearch Demo",$01101083$)

declare QuickSearch quicksearch!
declare BBjChildWindow wnd!

wnd! = mainWindow!.addChildWindow(mainWindow!.getAvailableControlID(),0,0,0,0,"",$00100800$,BBjAPI().getSysGui().getAvailableContext())
wnd!.setDockStyle("margin-left","15px")
wnd!.setDockStyle("margin-top","15px")
quicksearch! = new QuickSearch(wnd!,wnd!.getAvailableControlID())
quicksearch!.setCallback(QuickSearch.ON_EDIT_FINISHED,"on_edit_finished")
quicksearch!.setCallback(QuickSearch.ON_SUFFIX_CLICK,"on_suffix_click")
quicksearch!.setCallback(QuickSearch.ON_ITEM_SELECT,"on_item_select")
quicksearch!.getInputField().setPlaceholder("Type")
quicksearch!.setSuffixIcon("demo/assets/search.svg")
quicksearch!.getIconControl().setStyle("height","24px")
quicksearch!.getIconControl().setStyle("margin-left","15px")

declare BBjInputE testfield!

testfield! = wnd!.addInputE(wnd!.getAvailableControlID(),0,0,0,0,"")
testfield!.setStyle("margin-top","10px")

process_events
release

on_edit_finished:

declare ResultSet rs!
declare DataRow datarow!
declare BBjString data!

rs! = new ResultSet()
data! = cast(BBjString,cast(BBjCustomEvent,BBjAPI().getLastEvent()).getObject())

if data!.isEmpty() then
    return
FI

for i = 0 to 10
    datarow! = new DataRow()
    datarow!.addDataField("CUSTOMERDESC",new DataField(str(i) + str(i+1) +str(i+2) +str(i+3) + str(i+4) + str(i+5)))
    datarow!.addDataField("SERVICEORDERID",new DataField(str(i)))
    rs!.add(datarow!)
next i

declare ResultSet matchedData!

matchedData! = new ResultSet()
for i = 0 to rs!.size() -1
    declare DataRow myDr!

    myDr! = rs!.get(i)

    declare BBjString field!

    field! = str(myDr!.getDataField("CUSTOMERDESC"))

    if field! = data! or field!.contains(data!) then
        matchedData!.add(myDr!)
    FI
next i
REM quicksearch!.setSuggestionMask("[[CUSTOMERDESC]] ([[SERVICEORDERID]])")
quicksearch!.setSuggestionMask("[[CUSTOMERDESC]]")
quicksearch!.setSuggestions(matchedData!)
return
on_suffix_click:
a = msgbox("pressed Icon!")
return
on_item_select:

declare DataRow dr!

dr! = cast(DataRow,cast(BBjCustomEvent,BBjAPI().getLastEvent()).getObject())
quicksearch!.getInputField().setPlaceholder(str(dr!.getField("CUSTOMERDESC")))
quicksearch!.getInputField().setText("")
return