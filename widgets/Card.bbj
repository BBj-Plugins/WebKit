use ::widgets/BBjWebWidget.bbj::BBjWebWidget
rem BBjGridExWidget Package

class public Card extends BBjWebWidget

    field private BBjStaticText HeadlineCtrl!
    field private BBjStaticText BodyCtrl!
    field private BBjChildWindow ImageCtrl!
    field private BBjString Headline$
    field private BBjString Image$

    method public Card(BBjWindow wnd!)
        #super!.create(wnd!,wnd!.getAvailableControlID())
    methodend

    method public Card(BBjWindow wnd!, BBjInt id!)
        #super!.create(wnd!,id!)
    methodend

    rem /**
    rem  * @Override
    rem  * This method is called whenever the widget needs to be rendered
    rem  * @param Boolean f_init!: if TRUE the control is rendered for the first time so this method has to perform initial rendering
    rem  */
    method public void redraw(Boolean f_init!)
        
        card! = #getCanvas()
        
        if f_init!>0 then
        
        
            card!.addStyle("card")
            card!.addPanelStyle("cardpanel")


            
            #ImageCtrl! =  CAST(BBjChildWindow,card!.addChildWindow(card!.getAvailableControlID(),0,0,100,80,"",$00100800$,BBjAPI().getSysGui().getAvailableContext()))
            #ImageCtrl!.addPanelStyle("cardimg")

            textframe! =  CAST(BBjChildWindow,card!.addChildWindow(card!.getAvailableControlID(),0,0,100,80,"",$00100800$,BBjAPI().getSysGui().getAvailableContext()))
            textframe!.addPanelStyle("cardtext")
            #HeadlineCtrl! = CAST(BBjStaticText,textframe!.addStaticText(textframe!.getAvailableControlID(),0,0,100,80,""))
            #BodyCtrl! =  CAST(BBjStaticText,textframe!.addStaticText(textframe!.getAvailableControlID(),0,0,100,80,""))

        
        FI
        #HeadlineCtrl!.setText("<html><h3>"+#Headline$+"</h3></html>")
        #BodyCtrl!.setText("<html>"+str(#super!.getText())+"</html>")
        #ImageCtrl!.setPanelStyle("background-image",#Image$)
        
    methodend
    
    method public void setHeadline(BBjString text!)
        #Headline$= text!
        #redraw(0)
    methodend
    
    method public void setText(BBjString text!)
        #super!.setText(text!)
        #redraw(0)
    methodend
    
    method public void setImage(BBjString image$)
        #Image$ = image$
        #redraw(0)
    methodend
    
classend