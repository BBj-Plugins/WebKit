REM /**
REM  * DashboardCustomer.bbj
REM  * @author IanHupkes
REM  *
REM  */

use com.basiscomponents.db.DataRow
use com.basiscomponents.db.ResultSet

use ::BBjWidget/BBjWidget.bbj::BBjWidget
use ::WebKit/util/DynamicLoader.bbj::DynamicLoader

use ::WebKit/widgets/DashboardTasks/IDashboardTasksData.bbj::ITasksData
use ::Webkit/widgets/DashboardWidgetHeader/DashboardWidgetHeader.bbj::DashboardWidgetHeader
use ::WebKit/widgets/DashboardTasks/DashboardTasksCard.bbj::DashboardTasksCard

class public DashboardTasks extends BBjWidget
    field private BBjChildWindow window!
    field private DashboardWidgetHeader header!
    field private BBjChildWindow content!
    field private ITasksData tasksData!


    method public DashboardTasks(BBjWindow wnd!)
        #this!(wnd!,wnd!.getAvailableControlID(), new SampleTasksData())
    methodend

    method public DashboardTasks(BBjWindow wnd!, ITasksData tasksData!)
        #this!(wnd!,wnd!.getAvailableControlID(), tasksData!)
    methodend

    method public DashboardTasks(BBjWindow wnd!, BBjInt id!)
        #this!(wnd!,id!, new SampleTasksData())
    methodend

    method public DashboardTasks(BBjWindow wnd!, BBjInt id!, ITasksData tasksData!)
        DynamicLoader.addLocalCSS("WebKit/widgets/DashboardTasks/DashboardTasks.css")
        #tasksData! = tasksData!
        #super!.create(wnd!,id!)
    methodend
    
    method public void redraw(Boolean init!)
        #window! = #getCanvas()
        #window!.addPanelStyle("dashboardTasksPanelStyle")
        #window!.setDockStyle("width","100%")
        #window!.setDockStyle("height","100%")

        #header! = new DashboardWidgetHeader(#window!, "TASKS")

        #content! = #window!.addChildWindow(#window!.getAvailableControlID(),0,0,0,0,"",$00100800$,BBjAPI().getSysGui().getAvailableContext())
        #content!.addStyle("pushDownWidgetContent")
        
        contentMessage! = #content!.addStaticText(#content!.getAvailableControlID(),0,0,0,0,"Loading Tasks...")
        contentMessage!.addStyle("contentInfoMessage")
    methodend

    method public void buildContent()
        #content!.destroy()
        #content! = #window!.addChildWindow(#window!.getAvailableControlID(),0,0,0,0,"",$00100800$,BBjAPI().getSysGui().getAvailableContext())
 
        resultSet! = #tasksData!.getData()

        if  resultSet! <> null() AND  resultSet!.size() > 0
            for i = 1 to resultSet!.size()
                if i < 5
                    dr! = cast(DataRow, resultSet!.get(resultSet!.size() - i))
                    task! = new DashboardTasksCard(#content!, dr!.getFieldAsString("TASKTYPEDESC"), dr!.getFieldAsString("DESC"), "Due: " + cast(BBjString, dr!.getFieldAsString("DUE_DT")),"Priority: " + dr!.getFieldAsString("TASKPRIORITYDESC"))
                endif
            next i
            linkWrapper! = #content!.addChildWindow(#content!.getAvailableControlID(),0,0,0,0,"",$00100800$,BBjAPI().getSysGui().getAvailableContext())
            linkWrapper!.setDockStyle("padding", "1rem")
            
            link! = linkWrapper!.addStaticText(linkWrapper!.getAvailableControlID(),0,0,0,0,"To Tasks panel >")
            link!.addStyle("dashboardTasksLink")

            linkWrapper!.setCallback(BBjAPI.ON_MOUSE_DOWN,#this!,"onLinkClick")
        else  
            #content!.addStyle("pushDownWidgetContent") 
            contentMessage! = #content!.addStaticText(#content!.getAvailableControlID(),0,0,0,0,"No tasks to be shown")
            contentMessage!.addStyle("contentInfoMessage") 
        endif
    methodend

    method public void onLinkClick(BBjMouseDownEvent event!)
        a = msgbox("Tasks mainpage")
    methodend
classend