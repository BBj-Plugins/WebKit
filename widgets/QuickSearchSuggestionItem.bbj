use ::WebKit/widgets/BBjWebWidget.bbj::BBjWebWidget

use com.basiscomponents.db.DataRow

class public QuickSearchSuggestionItem extends BBjWebWidget

    field private BBjChildWindow window!
    
    field private BBjStaticText text!
    
    field private BBjString content! = ""
    
    field private DataRow dr!
    
    field public static BBjNumber ON_SUGGESTION_ITEM_CLICK = 700004
    
    field private Boolean selected! = 0
    
    field private BBjNumber index!
    
    method public QuickSearchSuggestionItem(BBjChildWindow wnd!)
        #super!.create(wnd!)
    methodend

    method public QuickSearchSuggestionItem(BBjWindow wnd!, BBjInt id!)
        #super!.create(wnd!,id!)
    methodend
    
    method public QuickSearchSuggestionItem(BBjWindow wnd!, BBjInt id!, BBjNumber index!)
        #super!.create(wnd!,id!)
        #index! = index!
    methodend    
    
    method public void redraw(Boolean init!)
        if init! 
            #window! = #getCanvas()
            #window!.setTabTraversable(1)
            #window!.setCallback(BBjAPI.ON_MOUSE_DOWN,#this!,"onSuggestionItemClick")
            #text! = #window!.addStaticText(#window!.getAvailableControlID(),0,0,0,0,#content!)
        else
            #text!.setText(#content!)
        endif
    methodend
    
    method public void onSuggestionItemClick(BBjMouseDownEvent event!)
        #fireEvent(#ON_SUGGESTION_ITEM_CLICK,#index!)
    methodend
    
    method public void setData(DataRow dr!, BBjString mask!,BBjString fieldName!)
        #dr! = dr!
        if mask! <> null() 
            REM Using the Mask instead of the fieldname
            REM TODO
        else 
            REM Using the fieldname since no mask provided
            #content! = str(#dr!.getDataField(fieldName!))
        endif
        #redraw(0)
    methodend
    
    method public DataRow getData()
        methodret #dr!
    methodend
    
    method public void destroy()
        if !#window!.isDestroyed()
            #window!.destroy()
        endif    
    methodend
    
    method public void selectItem()
        #selected! = 1
        #text!.setStyle("color","white")
        #window!.setStyle("background-color","blue")
    methodend
    
    method public void deselectItem()
        #selected! = 0
        #text!.removeStyle("color")
        #text!.removeStyle("background-color")
    methodend
classend