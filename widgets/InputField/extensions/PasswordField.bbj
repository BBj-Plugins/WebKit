use ::WebKit/util/DynamicLoader.bbj::DynamicLoader

use ::WebKit/widgets/InputField/InputField.bbj::InputField
use ::WebKit/widgets/InputField/IValidator.bbj::IValidator


class public PasswordField extends InputField

    field private Boolean isVisible! = Boolean.FALSE

    method public PasswordField (BBjChildWindow wnd!, IValidator validator!)
        #super!(wnd!, validator!)
    methodend

    method public void redraw(Boolean init!)
        if init! then
            #window! = #getCanvas()
            #window!.addPanelStyle("inputFieldPanelStyle")
            rem intermediate fix for tabindex defaulting to 0
            #window!.setAttribute("tabindex","-1")
            

            #label! = #window!.addStaticText( #window!.getAvailableControlID(),0,0,0,0,"")
            #label!.addStyle("inputFieldLabelStyle")
 
            #errorMessageWnd! = #window!.addChildWindow(#window!.getAvailableControlID(),0,0,0,0,"",$00108800$,BBjAPI().getSysGui().getAvailableContext())
            #errorMessageWnd!.addStyle("inputFieldSubTextWindow")
            rem intermediate fix for tabindex defaulting to 0
            #errorMessageWnd!.setAttribute("tabindex","-1")
                        
            #errorMessage! = #errorMessageWnd!.addStaticText(#window!.getAvailableControlID(),0,0,0,0,"")
            #errorMessage!.addStyle("inputFieldSubTextStyle")
                
            #inputField! = #window!.addEditBox(#window!.getAvailableControlID(),0,0,0,0,"",$0400$)
            #inputField!.addStyle("inputFieldStyle")
            #inputField!.addStyle("inputFieldStyleInputWrapper")
            #inputField!.addStyle("inputFieldStyleInput")
            #inputField!.setLeftMargin(0)

            #inputField!.setCallback(BBjAPI.ON_GAINED_FOCUS,#this!,"onFocusGained")
            #inputField!.setCallback(BBjAPI.ON_LOST_FOCUS,#this!,"onFocusLost")
            #inputField!.setCallback(BBjAPI.ON_EDIT_MODIFY,#this!,"onInputChange")  
        endif  
    methodend

    method public void onIconPressed(BBjMouseDownEvent event!)
        if #isVisible! then 
            #isVisible! = Boolean.FALSE
            #inputField!.setPasswordVisible(Boolean.FALSE)
            #super!.setSuffix("eye")
        else
            #isVisible! = Boolean.TRUE
            #inputField!.setPasswordVisible(Boolean.TRUE)
            #super!.setSuffix("eye-off")
        endif    
    methodend

classend