use ::WebKit/util/DynamicLoader.bbj::DynamicLoader

use ::WebKit/widgets/InputField/InputField.bbj::InputField
use ::WebKit/widgets/InputField/IValidator.bbj::IValidator


class public PasswordField extends InputField

    field private Boolean isVisible! = Boolean.FALSE

    method public PasswordField (BBjChildWindow wnd!, IValidator validator!)
        #this!(wnd!,wnd!.getAvailableControlID(), validator!)
    methodend

    method public PasswordField (BBjChildWindow wnd!, BBjInt id!, IValidator validator!)
        #super!(wnd!,id!, validator!)
    methodend

    method public void redraw(Boolean init!)
        if init! then
            #window! = #getCanvas()
            #window!.addPanelStyle("inputFieldPanelStyle")

            #label! = #window!.addStaticText( #window!.getAvailableControlID(),0,0,0,0,"")
            #label!.addStyle("inputFieldLabelStyle")
 
            #subTextWnd! = #window!.addChildWindow(#window!.getAvailableControlID(),0,0,0,0,"",$00100800$,BBjAPI().getSysGui().getAvailableContext())
            #subTextWnd!.addStyle("inputFieldSubTextWindow")
            #subText! = #subTextWnd!.addStaticText(#window!.getAvailableControlID(),0,0,0,0,"")
            #subText!.addStyle("inputFieldSubTextStyle")
                
            #inputField! = #window!.addEditBox(#window!.getAvailableControlID(),0,0,0,0,"",$0400$)
            #inputField!.addStyle("inputFieldStyle")
            #inputField!.addStyle("inputFieldStyleInputWrapper")
            #inputField!.addStyle("inputFieldStyleInput")
            #inputField!.setLeftMargin(0)

            #window!.setCallback(BBjAPI.ON_MOUSE_ENTER,#this!,"onHoverStart")
            #window!.setCallback(BBjAPI.ON_MOUSE_EXIT,#this!,"onHoverExit")
            #inputField!.setCallback(BBjAPI.ON_GAINED_FOCUS,#this!,"onFocusGained")
            #inputField!.setCallback(BBjAPI.ON_LOST_FOCUS,#this!,"onFocusLost")
            #inputField!.setCallback(BBjAPI.ON_EDIT_MODIFY,#this!,"onInputChange")  
        endif  
    methodend

    method public void onIconPressed(BBjMouseDownEvent event!)
        if #isVisible! then 
            #isVisible! = Boolean.FALSE
            #inputField!.setPasswordVisible(Boolean.FALSE)
            #super!.setSuffix("WebKit/framework/LoginDialog/assets/eye.png")
        else
            #isVisible! = Boolean.TRUE
            #inputField!.setPasswordVisible(Boolean.TRUE)
            #super!.setSuffix("WebKit/framework/LoginDialog/assets/eye-off.png")
        endif    
    methodend

classend