use ::BBjWidget/BBjWidget.bbj::BBjWidget
use ::WebKit/util/DynamicLoader.bbj::DynamicLoader
use ::WebKit/widgets/InputField/IValidator.bbj::IValidator
use ::WebKit/widgets/InputField/IValidator.bbj::RepeatPasswordValidator
use ::WebKit/util/ClientUtil.bbj::ClientUtil
use ::WebKit/util/Icons.bbj::Icons

class public InputField extends BBjWidget

    field protected BBjChildWindow window!
    field protected IValidator validator!
    
    field protected BBjStaticText label!
    field protected BBjEditBox inputField!
    field protected BBjChildWindow errorMessageWnd!
    field protected BBjStaticText errorMessage!
    field private BBjString suffixIconPath!
    field public BBjString id!
    field private BBjStaticText fieldIcon!
    field public static BBjNumber ON_INPUT_CHANGE = 4444

    field protected Boolean valid! = Boolean.FALSE

    method public InputField(BBjChildWindow wnd!, IValidator validator!)
         DynamicLoader.addLocalCSS("WebKit/widgets/InputField/InputField.css")

         #validator! = validator!
         #super!.create(wnd!, wnd!.getAvailableControlID())
    methodend

    method public InputField(BBjChildWindow wnd!)
        DynamicLoader.addLocalCSS("WebKit/widgets/InputField/InputField.css")

        #super!.create(wnd!, wnd!.getAvailableControlID())
    methodend
    
    method public void redraw(Boolean init!)
        if init! then
            #window! = #getCanvas()
            
            #window!.addPanelStyle("inputFieldPanelStyle")
            rem intermediate fix for tabindex defaulting to 0
            #window!.setAttribute("tabindex","-1")

            #label! = #window!.addStaticText( #window!.getAvailableControlID(),0,0,0,0,"")
            #label!.addStyle("inputFieldLabelStyle")
 
            #errorMessageWnd! = #window!.addChildWindow(#window!.getAvailableControlID(),0,0,0,0,"",$00118800$,BBjAPI().getSysGui().getAvailableContext())
            #errorMessageWnd!.addStyle("inputFieldSubTextWindow")
            #errorMessage! = #errorMessageWnd!.addStaticText(#window!.getAvailableControlID(),0,0,0,0,"")
            #errorMessage!.addStyle("inputFieldSubTextStyle")
                
            #inputField! = #window!.addEditBox(#window!.getAvailableControlID(),0,0,0,0,"")
            #inputField!.addStyle("inputFieldStyle")
            #inputField!.addStyle("inputFieldStyleInputWrapper")
            #inputField!.addStyle("inputFieldStyleInput")
            #inputField!.setLeftMargin(0)

            #inputField!.setCallback(BBjAPI.ON_GAINED_FOCUS,#this!,"onFocusGained")
            #inputField!.setCallback(BBjAPI.ON_LOST_FOCUS,#this!,"onFocusLost")
            #inputField!.setCallback(BBjAPI.ON_EDIT_MODIFY,#this!,"onInputChange")  
        endif  
    methodend


    rem ||||||||||||||||||||||||||||||||||||||||||||
    rem ||||||||||||| GETTERS & SETTERS ||||||||||||
    rem ||||||||||||||||||||||||||||||||||||||||||||

     method public IValidator getValidator()
        methodret #validator!
    methodend

    method public BBjEditBox getInputField()
        methodret #inputField! 
    methodend

    method public BBjString getInput()
        methodret #inputField!.getText()
    methodend

    method public void setInput(BBjString input$)
        #inputField!.setText(input$)    
    methodend

    method public void setLabel(BBjString label!)
        #label!.setText(label!)
    methodend

    method public void setId(BBjString id!)
        #id! = id!
    methodend

    method public void setPlaceHolder(BBjString placeholder!)
        #inputField!.setPlaceholder(placeholder!)
    methodend

    method public void setSuffix(BBjString suffixPath!)  
        #suffixIconPath! = suffixPath!
        #createIcon()
    methodend
    
    method public void setSuffixIcon(BBjString suffixIcon!)
        #suffixIconPath! = suffixIcon!
        #createIcon()
    methodend

    method public BBjString getSubText()
        methodret #errorMessage!.getText()
    methodend
    
    method public BBjString getId()
        methodret #id!
    methodend

    method public void setErrorMessage(BBjString errorMessage!)
        #errorMessage!.setText(errorMessage!)
    methodend

    method public Boolean isValid()
        methodret #valid!
    methodend

    rem ||||||||||||||||||||||||||||||||||||||||||||
    rem |||||||||||||||| CALLBACKS |||||||||||||||||
    rem ||||||||||||||||||||||||||||||||||||||||||||
 
    method private void createIcon()
        #fieldIcon! = Icons.getIconOnWindow(#window!, #suffixIconPath!)

        #fieldIcon!.addStyle("inputFieldIcon")
    methodend

    method public void onFocusGained(BBjGainedFocusEvent event!)
        #label!.addStyle("focus")
        #inputField!.addStyle("focus")   
    methodend
    
    method public void onFocusLost(BBjLostFocusEvent event!)
        #label!.removeStyle("focus")
        #inputField!.removeStyle("focus")
    methodend

    method public Boolean validate()
        if #validator! <> null() then
            #errorMessage!.setText(#validator!.validate(#inputField!.getText()))
            if #errorMessage!.getText() <> "" then 
                #inputField!.addStyle("errorMessage")
                #valid! = Boolean.FALSE 
            else 
                #inputField!.removeStyle("errorMessage")
                #valid! = Boolean.TRUE 
            endif
        endif
        methodret #valid!
    methodend

    method public void onInputChange(BBjEditModifyEvent event!)
        #validate()
        #fireEvent(#ON_INPUT_CHANGE, "")
    methodend
    
    method public void focus()
        #inputField!.focus()
    methodend 
    
classend
