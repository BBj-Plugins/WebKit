use ::BBjWidget/BBjWidget.bbj::BBjWidget
use ::WebKit/util/DynamicLoader.bbj::DynamicLoader

use ::WebKit/widgets/InputField/IValidator.bbj::IValidator
use ::WebKit/widgets/InputField/IValidator.bbj::RepeatPasswordValidator

use ::WebKit/framework/LoginDialog/LoginDialogWindow.bbj::LoginDialogWindow

class public InputField extends BBjWidget 

    field protected BBjChildWindow window!
    field protected IValidator validator!
    
    field protected BBjStaticText label!
    field protected BBjEditBox inputField!
    field protected BBjChildWindow suffixWrapper!
    field protected BBjChildWindow subTextWnd!
    field protected BBjStaticText subText!


    method public InputField(BBjChildWindow wnd!, IValidator validator!)
         #this!(wnd!,wnd!.getAvailableControlID(), validator!)
    methodend
    
    method public InputField(BBjChildWindow wnd!, BBjInt id!, IValidator validator!)
        DynamicLoader.addLocalCSS("WebKit/widgets/InputField/InputField.css")

        #validator! = validator!
        #super!.create(wnd!,id!)
    methodend
    
    method public void redraw(Boolean init!)
        if init! then
            #window! = #getCanvas()
            #window!.addPanelStyle("inputFieldPanelStyle")

            #label! = #window!.addStaticText( #window!.getAvailableControlID(),0,0,0,0,"")
            #label!.addStyle("inputFieldLabelStyle")
 
            #subTextWnd! = #window!.addChildWindow(#window!.getAvailableControlID(),0,0,0,0,"",$00100800$,BBjAPI().getSysGui().getAvailableContext())
            #subTextWnd!.addStyle("inputFieldSubTextWindow")
            #subText! = #subTextWnd!.addStaticText(#window!.getAvailableControlID(),0,0,0,0,"")
            #subText!.addStyle("inputFieldSubTextStyle")
                
            #inputField! = #window!.addEditBox(#window!.getAvailableControlID(),0,0,0,0,"")
            #inputField!.addStyle("inputFieldStyle")
            #inputField!.addStyle("inputFieldStyleInputWrapper")
            #inputField!.addStyle("inputFieldStyleInput")
            #inputField!.setLeftMargin(0)

            #window!.setCallback(BBjAPI.ON_MOUSE_ENTER,#this!,"onHoverStart")
            #window!.setCallback(BBjAPI.ON_MOUSE_EXIT,#this!,"onHoverExit")
            #inputField!.setCallback(BBjAPI.ON_GAINED_FOCUS,#this!,"onFocusGained")
            #inputField!.setCallback(BBjAPI.ON_LOST_FOCUS,#this!,"onFocusLost")
            #inputField!.setCallback(BBjAPI.ON_EDIT_MODIFY,#this!,"onInputChange")  
        endif  
    methodend


    rem ||||||||||||||||||||||||||||||||||||||||||||
    rem ||||||||||||| GETTERS & SETTERS ||||||||||||
    rem ||||||||||||||||||||||||||||||||||||||||||||

     method public IValidator getValidator()
        methodret #validator!
    methodend

    method public BBjEditBox getInputField()
        methodret #inputField! 
    methodend

    rem method public BBjString getText()
    rem     methodret #inputField!.getText()
    rem methodend

    method public void setLabel(BBjString label!)
        #label!.setText(label!)
    methodend

    method public void setPlaceHolder(BBjString placeholder!)
        #inputField!.setPlaceholder(placeholder!)
    methodend


    method public void setSuffix(BBjString suffixPath!)  
        if #suffixWrapper! <> null() then
            #suffixWrapper!.destroy()
        endif 

        #suffixWrapper! = #window!.addChildWindow(#window!.getAvailableControlID(),0,0,0,0,"",$00100800$,BBjAPI().getSysGui().getAvailableContext())
        #suffixWrapper!.setDockStyle("grid-row-start","2")
        #suffixWrapper!.setDockStyle("grid-column-start","2")
        #suffixWrapper!.setDockStyle("height","fit-content")
        #suffixWrapper!.setDockStyle("width","fit-content")
        #suffixWrapper!.setDockStyle("align-self","end")
        #suffixWrapper!.setDockStyle("justify-self","end")
        suffixIcon! = #suffixWrapper!.addImageCtrl(#suffixWrapper!.getAvailableControlID(),0,0,0,0,suffixPath!)
   
        #suffixWrapper!.setCallback(BBjAPI.ON_MOUSE_DOWN,#this!,"onIconPressed")
    methodend

    method public void setSubText(BBjString subText!)
        #subText!.setText(subText!)
    methodend



    rem ||||||||||||||||||||||||||||||||||||||||||||
    rem |||||||||||||||| CALLBACKS |||||||||||||||||
    rem ||||||||||||||||||||||||||||||||||||||||||||

    method public void onHoverStart(BBjMouseEnterEvent event!)
        #label!.addStyle("hover")
    methodend
    
    method public void onHoverExit(BBjMouseExitEvent event!)
        #label!.removeStyle("hover")
    methodend

    method public void onFocusGained(BBjGainedFocusEvent event!)
        #label!.addStyle("focus")
        #inputField!.addStyle("focus")   
    methodend
    
    method public void onFocusLost(BBjLostFocusEvent event!)
        #label!.removeStyle("focus")
        #inputField!.removeStyle("focus")
    methodend

    method public void onInputChange(BBjEditModifyEvent event!)
        if #validator! <> null() then
            #subText!.setText(#validator!.validate(event!.getText()))
        endif
    methodend 
    
    method public void onIconPressed(BBjMouseDownEvent event!)
        rem to be overwritten by PasswordField
    methodend

classend
