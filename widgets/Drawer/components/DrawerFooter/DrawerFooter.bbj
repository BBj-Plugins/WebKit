use ::WebKit/util/DynamicLoader.bbj::DynamicLoader
use ::BBjWidget/BBjWidget.bbj::BBjWidget
use ::WebKit/framework/LoginDialog/LoginDialog.bbj::LoginDialog

use ::WebKit/model/DrawerModel.bbj::DrawerModel
use ::WebKit/widgets/common/CircularAvatar/CircularAvatar.bbj::CircularAvatar
use ::WebKit/widgets/common/Overlay/Overlay.bbj::Overlay
use ::WebKit/widgets/Drawer/components/DrawerMenuTile/DrawerMenuTile.bbj::DrawerMenuTile
use ::WebKit/model/DrawerMenuTileEntry.bbj::DrawerMenuTileEntry
use ::WebKit/model/Icon.bbj::Icon

class public DrawerFooter extends BBjWidget implements Icon

    field private BBjChildWindow window!
    
    field private BBjString title!
    
    field private BBjString subtitle!
    
    field private BBjChildWindow parent!
    
    field private BBjChildWindow overlay!
    
    field private Overlay overlayWidget!

    field private BBjVector tiles! = new BBjVector()

    field private DrawerModel drawerDataModel!

    field public static BBjNumber ON_LOGOUT = 15321

    field public static BBjNumber ON_FOOTER_TILE_PRESSED = 14321
    
    method public DrawerFooter(BBjWindow parent!, BBjChildWindow drawer!,DrawerModel drawerDataModel!)
        DynamicLoader.addLocalCSS("WebKit/widgets/Drawer/components/DrawerFooter/DrawerFooter.css")
        #window! = parent!.addChildWindow(parent!.getAvailableControlID(),0,0,0,0,"",$00108800$,BBjAPI().getSysGui().getAvailableContext())
        #window!.addPanelStyle("drawerFooterPanelStyle")
        #window!.addPanelStyle("drawerFooterPanelAnimationOpen")
        #window!.addPanelStyle("drawerFooterPanelAnimationClose")
        
        #window!.setCallback(BBjAPI.ON_MOUSE_DOWN,#this!,"onFooterPressed")
        #drawerDataModel! = drawerDataModel!
        #title! = drawerDataModel!.getFooterTitle()
        #subtitle! = drawerDataModel!.getFooterSubTitle()
        #tiles! = drawerDataModel!.getFooterMenuTiles()
        #parent!= drawer!
        #setCanvas(#window!)
        #redraw(1)
    methodend
    
    method public void redraw(Boolean init!)
        declare BBjStaticText title!
        declare BBjStaticText subtitle!
        declare CircularAvatar avatar!
        declare BBjChildWindow wrapper!
        declare BBjStaticText icon!
        
        if init! then 
            avatar! = new CircularAvatar(#window!, #drawerDataModel!.getAvatarSize(),#drawerDataModel!.getAvatarUrl())
            wrapper! = #window!.addChildWindow(#window!.getAvailableControlID(),0,0,0,0,"",$00108800$,BBjAPI().getSysGui().getAvailableContext())
            wrapper!.addPanelStyle("drawerFooterWrapperWindowClosed")
            wrapper!.addPanelStyle("drawerFooterWrapperWindowSmall")
            wrapper!.addPanelStyle("drawerFooterWrapperWindowLarge")
            wrapper!.setPanelStyle("display","grid")
            wrapper!.setPanelStyle("height","100%")
            wrapper!.setDockStyle("margin-left","12px")
            wrapper!.setDockStyle("height","100%")
            wrapper!.setStyle("height","100%")
            
            title! = wrapper!.addStaticText(wrapper!.getAvailableControlID(),0,0,0,0,#title!)
            title!.addStyle("drawerFooterTitleStyling")
            
            subtitle! = wrapper!.addStaticText(wrapper!.getAvailableControlID(),0,0,0,0,#subtitle!)
            subtitle!.addStyle("drawerFooterSubtitleStyling")
            
            icon! = #setIcon(#window!, "chevron-down")
            icon!.addStyle("footerIcon")
        endif
    methodend
    
    method public void onFooterPressed(BBjMouseDownEvent event!)
        #createOverlay()
        #overlayWidget! = new Overlay(#parent!,#overlay!)
        #overlayWidget!.setCallback(Overlay.ON_OVERLAY_DISSMISSED,#this!,"onOverlayDissmiss")
    methodend
    
    method private void createOverlay()
        declare BBjChildWindow contentWrapper!
        declare BBjChildWindow settings!
        declare BBjChildWindow logout!
        declare BBjImageCtrl settingsIcon!
        declare BBjImageCtrl logoutIcon!
        declare BBjStaticText settingsText!
        declare BBjStaticText logoutText!
        
        REM TODO improve code quality and maybe add option to create more
        
        #overlay! = #parent!.addChildWindow(#parent!.getAvailableControlID(),0,0,0,0,"",$00108800$,BBjAPI().getSysGui().getAvailableContext())
        #overlay!.addPanelStyle("drawerFooterOverlayPanelStyle")
        #overlay!.addPanelStyle("drawerFooterOverlayClosedPanelStyle")
        
        contentWrapper! = #overlay!.addChildWindow(#overlay!.getAvailableControlID(),0,0,0,0,"",$00108800$,BBjAPI().getSysGui().getAvailableContext())
        contentWrapper!.addPanelStyle("drawerFooterOverlayContentWrapper")

        declare DrawerMenuTileEntry entry!
        declare BBjChildWindow spacer!
        templateRows! = ""
        
        for i = 0 to #tiles!.size() -1
            entry! = cast(DrawerMenuTileEntry,#tiles!.get(i))
            tile! = new DrawerMenuTile(contentWrapper!,entry!.getID(),entry!.getIconPath(),entry!.getTitle())
            tile!.setCallback(DrawerMenuTile.ON_DRAWER_TILE_PRESSED, #this!, "onMenuItemClick")
            templateRows! = templateRows! + " 48px" 
            if i <> #tiles!.size() -1 then 
                templateRows! = templateRows! + " 12px"
                spacer! = contentWrapper!.addChildWindow(contentWrapper!.getAvailableControlID(),0,0,0,0,"",$00108800$,BBjAPI().getSysGui().getAvailableContext())
                spacer!.setDockStyle("height","12px")
            endif
        next i
        contentWrapper!.setPanelStyle("grid-template-rows",templateRows!)
    methodend
    
    method public void onMenuItemClick(BBjCustomEvent ev!)
        id%  = num(str(ev!.getObject()))
        #fireEvent(#ON_FOOTER_TILE_PRESSED, id%)
    methodend

    
    method public void onLogout(BBjMouseDownEvent event!)
        #overlayWidget!.onOverlayDissmiss(null())
        login! = new LoginDialog()
        login!.clearRememberToken()
        #fireEvent(#ON_LOGOUT, "")
    methodend
    
    method public void onOverlayDissmiss(BBjCustomEvent event!)
        if #overlay! <> null() AND ! #overlay!.isDestroyed() then
            #overlay!.destroy()
        fi
        #overlay! = null()
    methodend
classend
