use ::WebKit/util/DynamicLoader.bbj::DynamicLoader
use ::BBjWidget/BBjWidget.bbj::BBjWidget
use ::WebKit/model/DrawerModel.bbj::DrawerModel
use ::WebKit/model/DrawerMenuTileEntry.bbj::DrawerMenuTileEntry
use ::WebKit/widgets/Drawer/components/DrawerMenuTile/DrawerMenuTile.bbj::DrawerMenuTile

use ::WebKit/widgets/Drawer/components/DrawerCloseIcon/DrawerCloseIcon.bbj::DrawerCloseIcon

class public DrawerHeader extends BBjWidget 
    
    field private BBjChildWindow window!

    field private BBjChildWindow logoContainer!

    field private BBjChildWindow contentWrapper!
    
    field private BBjString logo!
    
    field private BBjString smallLogo!

    field private BBjString closeIcon!
    
    field private DrawerModel drawerDataModel!
    
    field private BBjVector tiles! = new BBjVector()
    
    field public static BBjNumber ON_DRAWER_CLOSE_ICON_PUSH = DrawerCloseIcon.getON_DRAWER_CLOSE_ICON_PUSH()

    field public static BBjNumber ON_DRAWER_HEADER_TILE_PRESSED = 123456
    
    method public DrawerHeader(BBjWindow parent!, BBjString logo!, BBjString smallLogo!, BBjString closeIcon!,DrawerModel drawerDataModel!)
        DynamicLoader.addLocalCSS("WebKit/widgets/Drawer/components/DrawerHeader/DrawerHeader.css")
        #window! = parent!.addChildWindow(parent!.getAvailableControlID(),0,0,0,0,"",$00100800$,BBjAPI().getSysGui().getAvailableContext())

        #logoContainer! = #window!.addChildWindow(#window!.getAvailableControlID(), 0, 0, 0, 0, "", $00100800$, BBjAPI().getSysGui().getAvailableContext())
        #logoContainer!.addPanelStyle("drawerHeaderPanelStyle")
        #logoContainer!.addPanelStyle("drawerHeaderPanelStyleOpen")
        #logoContainer!.addPanelStyle("drawerHeaderPanelStyleClosed")

        #contentWrapper! = #window!.addChildWindow(#window!.getAvailableControlID(),0,0,0,0,"",$00100800$,BBjAPI().getSysGui().getAvailableContext())
        #contentWrapper!.addPanelStyle("drawerFooterOverlayContentWrapper")
        #contentWrapper!.addStyle("headerMenuContentWrapper")

        #setCanvas(#window!)
        #logo!= logo!
        #smallLogo! = smallLogo!
        #closeIcon! = closeIcon!
        #drawerDataModel! = drawerDataModel!
        #tiles! = #drawerDataModel!.getHeaderMenuTiles()
        #redraw(1)
    methodend
    
    method public void redraw(Boolean init!)
        declare BBjImageCtrl logo!
        declare BBjImageCtrl smallLogo!
        declare DrawerCloseIcon iconBtn!
        if init!
            REM TODO
            logo! = #logoContainer!.addImageCtrl(#logoContainer!.getAvailableControlID(),0,0,0,0,#logo!)
            REM if set to fit-content it jumps from 64px to 66 when clicking on the dashboard
            logo!.addStyle("drawerHeaderLogo")
            logo!.addStyle("drawerHeaderLogoClosed")
            
            smallLogo! = #logoContainer!.addImageCtrl(#logoContainer!.getAvailableControlID(),0,0,0,0,#smallLogo!)
            smallLogo!.setStyle("display","none")
            smallLogo!.addStyle("drawerHeaderLogoSmallClosed")
            smallLogo!.addStyle("drawerHeaderLogoSmallOpen")
            
            iconBtn! = new DrawerCloseIcon(#logoContainer!, #closeIcon!)
            iconBtn!.setCallback(DrawerCloseIcon.ON_DRAWER_CLOSE_ICON_PUSH,#this!,"onDrawerIconPush")

            declare DrawerMenuTileEntry entry!
            declare BBjChildWindow spacer!
            templateRows! = ""
            
            i = 0
            while i < #tiles!.size()
                entry! = cast(DrawerMenuTileEntry,#tiles!.get(i))
                tile! = new DrawerMenuTile(#contentWrapper!,entry!.getID(),entry!.getIconPath(),entry!.getTitle())
                tile!.setCallback(DrawerMenuTile.ON_DRAWER_TILE_PRESSED, #this!, "onMenuItemClick")
                templateRows! = templateRows! + " 48px" 
                if i <> #tiles!.size() -1 then 
                    templateRows! = templateRows! + " 12px"
                    spacer! = #contentWrapper!.addChildWindow(#contentWrapper!.getAvailableControlID(),0,0,0,0,"",$00100800$,BBjAPI().getSysGui().getAvailableContext())
                    spacer!.setDockStyle("height","12px")
                endif
            i = i + 1
            wend
            #contentWrapper!.setPanelStyle("grid-template-rows",templateRows!)
        endif    
    methodend
    
    method public void onDrawerIconPush(BBjCustomEvent event!)
        #fireEvent(#ON_DRAWER_CLOSE_ICON_PUSH,"")
    methodend

    method public void onMenuItemClick(BBjCustomEvent ev!)
        #fireEvent(#ON_DRAWER_HEADER_TILE_PRESSED, ev!.getObject())
    methodend
classend
