use ::WebKit/util/DynamicLoader.bbj::DynamicLoader
use ::BBjWidget/BBjWidget.bbj::BBjWidget
use ::WebKit/model/DrawerModel.bbj::DrawerModel
use ::WebKit/model/DrawerMenuTileEntry.bbj::DrawerMenuTileEntry
use ::WebKit/widgets/Drawer/components/DrawerMenuTile/DrawerMenuTile.bbj::DrawerMenuTile

class public DrawerHeader extends BBjWidget 
    
    field private BBjWindow window!

    field private BBjChildWindow logoContainer!

    field private BBjChildWindow contentWrapper!
    
    field private BBjString logo!
    
    field private BBjString smallLogo!
    
    field private DrawerModel drawerDataModel!
    
    field private BBjVector tiles! = new BBjVector()
    
    field public static BBjNumber ON_DRAWER_CLOSE_ICON_PUSH = 144444

    field public static BBjNumber ON_DRAWER_OPEN_ICON_PUSH = 144445

    field public static BBjNumber ON_DRAWER_HEADER_TILE_PRESSED = 123456
    
    method public DrawerHeader(BBjWindow parent!, DrawerModel drawerDataModel!)
        DynamicLoader.addLocalCSS("WebKit/widgets/Drawer/components/DrawerHeader/DrawerHeader.css")
        rem #window! = parent!.addChildWindow(parent!.getAvailableControlID(),0,0,0,0,"",$00108800$,BBjAPI().getSysGui().getAvailableContext())
        #window! = parent!
        #logoContainer! = #window!.addChildWindow(#window!.getAvailableControlID(), 0, 0, 0, 0, "", $00108800$, BBjAPI().getSysGui().getAvailableContext())
        #logoContainer!.addPanelStyle("drawerHeaderPanelStyle")
        #logoContainer!.addPanelStyle("drawerHeaderPanelStyleOpen")
        #logoContainer!.addPanelStyle("drawerHeaderPanelStyleClosed")

        #contentWrapper! = #window!.addChildWindow(#window!.getAvailableControlID(),0,0,0,0,"",$00108800$,BBjAPI().getSysGui().getAvailableContext())
        #contentWrapper!.addPanelStyle("drawerFooterOverlayContentWrapper")
        #contentWrapper!.addStyle("headerMenuContentWrapper")

        rem #setCanvas(#window!)
        #drawerDataModel! = drawerDataModel!
        #logo!= #drawerDataModel!.getLogoUrl()
        #smallLogo! = #drawerDataModel!.getLogoSmallUrl()
        #tiles! = #drawerDataModel!.getHeaderMenuTiles()
        #redraw(1)
    methodend
    
    method public void redraw(Boolean init!)
        declare BBjImageCtrl logo!
        declare BBjImageCtrl smallLogo!
        declare BBjButton closeIconButton!
        declare BBjButton openIconButton!
        if init!
            REM TODO
            logo! = #logoContainer!.addImageCtrl(#logoContainer!.getAvailableControlID(),0,0,0,0,#logo!)
            REM if set to fit-content it jumps from 64px to 66 when clicking on the dashboard
            logo!.addStyle("drawerHeaderLogo")
            logo!.addStyle("drawerHeaderLogoClosed")
            
            smallLogo! = #logoContainer!.addImageCtrl(#logoContainer!.getAvailableControlID(),0,0,0,0,#smallLogo!)
            smallLogo!.setStyle("display","none")
            smallLogo!.addStyle("drawerHeaderLogoSmallClosed")
            smallLogo!.addStyle("drawerHeaderLogoSmallOpen")
            
            rem iconBtn! = new DrawerCloseIcon(#logoContainer!, #closeIcon!)
            if #drawerDataModel!.getShowHeaderIcon() then
                openIconButton! = #logoContainer!.addButton(#logoContainer!.getAvailableControlID(), 0,0,0,0, #drawerDataModel!.getOpenIcon())
                openIconButton!.setAttribute("theme", "")
                openIconButton!.addStyle("drawerOpenIcon")
                openIconButton!.addStyle("drawerRoundButton")
                openIconButton!.setCallback(BBjAPI.ON_BUTTON_PUSH, #this!,"onCloseIconPush")
    
                closeIconButton! = #logoContainer!.addButton(#logoContainer!.getAvailableControlID(), 0,0,0,0, #drawerDataModel!.getCloseIcon())
                closeIconButton!.setAttribute("theme", "")
                closeIconButton!.addStyle("drawerRoundButton")
                closeIconButton!.addStyle("drawerCloseIcon")
                closeIconButton!.setCallback(BBjAPI.ON_BUTTON_PUSH, #this!,"onOpenIconPush")
            fi
            declare DrawerMenuTileEntry entry!
            declare BBjChildWindow spacer!
            templateRows! = ""
            
            i = 0
            while i < #tiles!.size()
                entry! = cast(DrawerMenuTileEntry,#tiles!.get(i))
                tile! = new DrawerMenuTile(#contentWrapper!,entry!.getID(),entry!.getIconPath(),entry!.getTitle())
                tile!.setCallback(DrawerMenuTile.ON_DRAWER_TILE_PRESSED, #this!, "onMenuItemClick")
                templateRows! = templateRows! + " 48px" 
                if i <> #tiles!.size() -1 then 
                    templateRows! = templateRows! + " 12px"
                    spacer! = #contentWrapper!.addChildWindow(#contentWrapper!.getAvailableControlID(),0,0,0,0,"",$00108800$,BBjAPI().getSysGui().getAvailableContext())
                    spacer!.setDockStyle("height","12px")
                endif
            i = i + 1
            wend
            #contentWrapper!.setPanelStyle("grid-template-rows",templateRows!)
        endif    
    methodend
    
    method public void onOpenIconPush(BBjButtonPushEvent event!)
        #fireEvent(#ON_DRAWER_CLOSE_ICON_PUSH, null())
    methodend

    method public void onCloseIconPush(BBjButtonPushEvent event!)
        #fireEvent(#ON_DRAWER_OPEN_ICON_PUSH, null())
    methodend

    method public void onMenuItemClick(BBjCustomEvent ev!)
        #fireEvent(#ON_DRAWER_HEADER_TILE_PRESSED, ev!.getObject())
    methodend
classend
