use ::WebKit/util/DynamicLoader.bbj::DynamicLoader
use ::BBjWidget/BBjWidget.bbj::BBjWidget

use ::WebKit/widgets/Drawer/components/DrawerCloseIcon/DrawerCloseIcon.bbj::DrawerCloseIcon

class public DrawerHeader extends BBjWidget 
    
    field private BBjChildWindow window!
    
    field private BBjString logo!
    
    field private BBjString smallLogo!

    field private BBjString closeIcon!
    
    field public static BBjNumber ON_DRAWER_CLOSE_ICON_PUSH = DrawerCloseIcon.getON_DRAWER_CLOSE_ICON_PUSH()
    
    method public DrawerHeader(BBjWindow parent!, BBjString logo!, BBjString smallLogo!, BBjString closeIcon!)
        DynamicLoader.addLocalCSS("WebKit/widgets/Drawer/components/DrawerHeader/DrawerHeader.css")
        #window! = parent!.addChildWindow(parent!.getAvailableControlID(),0,0,0,0,"",$00100800$,BBjAPI().getSysGui().getAvailableContext())
        #window!.addPanelStyle("drawerHeaderPanelStyle")
        #window!.addPanelStyle("drawerHeaderPanelStyleOpen")
        #window!.addPanelStyle("drawerHeaderPanelStyleClosed")
        #setCanvas(#window!)
        #logo!= logo!
        #smallLogo! = smallLogo!
        #closeIcon! = closeIcon!
        #redraw(1)
    methodend
    
    method public void redraw(Boolean init!)
        declare BBjImageCtrl logo!
        declare BBjImageCtrl smallLogo!
        declare DrawerCloseIcon iconBtn!
        if init!
            REM TODO
            logo! = #window!.addImageCtrl(#window!.getAvailableControlID(),0,0,0,0,#logo!)
            REM if set to fit-content it jumps from 64px to 66 when clicking on the dashboard
            logo!.addStyle("drawerHeaderLogo")
            logo!.addStyle("drawerHeaderLogoClosed")
            
            smallLogo! = #window!.addImageCtrl(#window!.getAvailableControlID(),0,0,0,0,#smallLogo!)
            smallLogo!.setStyle("display","none")
            smallLogo!.addStyle("drawerHeaderLogoSmallClosed")
            smallLogo!.addStyle("drawerHeaderLogoSmallOpen")
            
            iconBtn! = new DrawerCloseIcon(#window!, #closeIcon!)
            iconBtn!.setCallback(DrawerCloseIcon.ON_DRAWER_CLOSE_ICON_PUSH,#this!,"onDrawerIconPush")
        endif    
    methodend
    
    method public void onDrawerIconPush(BBjCustomEvent event!)
        #fireEvent(#ON_DRAWER_CLOSE_ICON_PUSH,"")
    methodend
classend
