use ::WebKit/widgets/Drawer/Drawer.bbj::Drawer
use ::WebKit/widgets/ListTile/ListTile.bbj::ListTile
use ::WebKit/model/DrawerMenuTileEntry.bbj::DrawerMenuTileEntry
use ::WebKit/widgets/TilesTextHeader/TilesTextHeader.bbj::TilesTextHeader
use ::WebKit/model/DrawerModel.bbj::DrawerModel
use ::WebKit/model/Menu.bbj::MenuItem
use ::WebKit/model/Menu.bbj::Menu
use ::WebKit/util/DynamicLoader.bbj::DynamicLoader
use ::WebKit/widgets/Drawer/components/DrawerHeader/AlternativeDrawerHeader.bbj::AlternativeDrawerHeader
use ::WebKit/widgets/Drawer/components/DrawerHeader/DrawerHeader.bbj::DrawerHeader

class public AlternativeDrawer extends Drawer
    
    method public AlternativeDrawer(BBjWindow parent!, DrawerModel dataModel!, Menu menu!)
        
        #super!(parent!,dataModel!,menu!)
        DynamicLoader.addLocalCSS("WebKit/widgets/Drawer/AlternativeDrawer.css")
        
    methodend
    rem @Override
    
    method public void buildDrawerTiles(BBjVector tiles!, BBjWindow tilesWrapper!)
            #buildTiles(#menu!.getRoot().getNodeId(),0,tilesWrapper!)
            
    methodend
    
    method public void buildTiles(BBjNumber nodeId,BBjNumber i,BBjWindow tilesWrapper!)
        declare MenuItem node!
        declare MenuItem item!
        node! = #menu!.getItem(nodeId)
        v! = node!.getChildren()
        it! = v!.iterator()
       
        templateRows! = "40px"
        rem escape
        while it!.hasNext()
            item! = cast(MenuItem,it!.next())
            id%  = item!.getNodeId()
            
            if item!.getCaption() <> "404"
                declare ListTile tile!
                tile! = new ListTile(tilesWrapper!)
                tile!.setText(item!.getCaption())
                tile!.setStyle("padding-left",str(5 + (i * 20)) + "px")
                tile!.setCallback(ListTile.ON_TEXTPART_CLICK,#this!,"onMenuItemClick")
                rem tile!.setCallback(ListTile.ON_FAVORITES_BUTTON_CLICK,#this!,"setFavorite")
                
                tile!.enableLetterField(0)
                
                tile!.toggleFavoritesButton(0)
                
                if item!.getIcon() <> "" AND item!.getIcon() <> null() then
                
                    tile!.setIcon(item!.getIcon())
                endif
                tile!.setItemId(item!.getNodeId())
                           
                #MenuItemTiles!.put(item!.getNodeId(),tile!)
                #buildTiles(id%,i+1,tilesWrapper!)
                if i > 0 then
                    tile!.setChild(1)
                    tile!.setVisible(0)
                endif
                if item!.hasChildren() then
                   tile!.setHeader()
                endif 
                templateRows! = templateRows! + " 48px" 
                rem templateRows! = templateRows! + " 12px"
                rem spacer! = tilesWrapper!.addChildWindow(tilesWrapper!.getAvailableControlID(),0,0,0,0,"",$00108800$,BBjAPI().getSysGui().getAvailableContext())
                rem spacer!.setDockStyle("height","12px")
            fi
        wend      
        
       
        
    methodend
    
    rem @Override
    method public void buildHeader()
            header! = new AlternativeDrawerHeader(#window!, #drawerDataModel!)
            

            header!.setCallback(DrawerHeader.ON_DRAWER_HEADER_TILE_PRESSED, #this!, "onHeaderMenuItemClick")
    methodend
    
    rem @Override
    method public void closeDrawer(BBjCustomEvent event!)
        #window!.addStyle("drawerClosed")
        #window!.removeStyle("drawerOpen")
        #window!.setVisible(0)
        
        #shadowWindow!.removeStyle("drawerShadow")
        #open! = !#open!
        #fireEvent(#ON_DRAWER_STATE_CHANGED, 0)
    methodend
    
    rem @Override
    method public void openDrawer(BBjCustomEvent event!)
        #window!.addStyle("drawerOpen")
        #window!.removeStyle("drawerClosed")
        #window!.setVisible(1)
        #open! = !#open!
        #fireEvent(#ON_DRAWER_STATE_CHANGED, 1)
    methodend
    
    method public void onMenuItemClick(BBjCustomEvent ev!)
        
        declare auto MenuItem node!
        declare auto MenuItem item!
        declare auto ListTile tile!
        declare auto BBjNumber nodeId
        nodeId=ev!.getObject()
        
        node!=#menu!.getItem(nodeId)
        if node!.hasChildren() then
            v! = node!.getChildren()
            it! = v!.iterator()
            while it!.hasNext()
                item! = it!.next()
                id%  = item!.getNodeId()            
                tile! = #MenuItemTiles!.get(id%)
                tile!.setVisible(!tile!.isVisible())
            
        wend
        else
            #fireEvent(#ON_DRAWER_TILE_PRESSED, ev!.getObject())
        fi
    methodend
classend