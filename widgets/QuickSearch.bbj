use ::WebKit/widgets/BBjWebWidget.bbj::BBjWebWidget

use com.basiscomponents.db.ResultSet
use com.basiscomponents.db.DataRow

use java.util.regex.Matcher
use java.util.regex.Pattern
use java.util.Arrays

class public QuickSearch extends BBjWebWidget

    field private BBjInputE inputE!
    
    field private BBjChildWindow iconWrapper!
    
    field private BBjChildWindow suggestionsWindow!
    
    field private BBjImageCtrl icon!
    
    field private BBjChildWindow window!
    
    field private BBjNumber time! = 0.5
    field private BBjString suggestionHeight! = "250px"
    field private BBjString suggestionMask!
    field private BBjVector suggestionItems! = new BBjVector()
    field private BBjNumber selectedSuggestionItem = 0
    
    field private DataRow selectedItem!
    
    REM TODO
    field public static BBjNumber ON_SUFFIX_CLICK = 700001
    field public static BBjNumber ON_FETCH_SUGGESTIONS = 700002
    field public static BBjNumber ON_ITEM_SELECT = 700003
    
    method public QuickSearch(BBjChildWindow wnd!)
        #super!.create(wnd!)
    methodend

    method public QuickSearch(BBjWindow wnd!, BBjInt id!)
        #super!.create(wnd!,id!)
    methodend
    
    method public void redraw(Boolean init!)
        if init! then 
            rem workaround for ON_INPUT_KEYPRESS not yet implemented in DWC
            #window! = #getCanvas().addChildWindow(777,1,1,1,1,"",$20100800$,BBjAPI().getSysGui().getAvailableContext())
            #window! .setCallback(BBjAPI.ON_KEYPRESS,#this!,"onKeyPress")
            
            #window!.setPanelStyle("display","grid")
            #window!.setPanelStyle("grid-template-columns","max-content max-content")
           
            #inputE! = #window!.addInputE(#window!.getAvailableControlID(),0,0,0,0,"")
            #inputE!.setStyle("grid-column-start","1")
            #inputE!.setStyle("grid-row-start","1")
            #inputE!.setCallback(BBjAPI.ON_EDIT_MODIFY,#this!,"onEditModify")
REM             #inputE!.setCallback(BBjAPI.ON_INPUT_KEYPRESS,#this!,"onKeyPress")
        endif
    methodend
    
    method public void onKeyPress(BBjKeypressEvent event!)
    
        if event!.getKeyCode() = 13 then
            rem enter
            item! = cast(QuickSearchSuggestionItem,#suggestionItems!.get(#selectedSuggestionItem))
            data! = item!.getData()
            
            #fireEvent(#ON_ITEM_SELECT,data!)
            #selectedItem! = CAST(DataRow,data!)
            
            
            #suggestionsWindow!.destroy()
            #suggestionsWindow! = null()
            #suggestionItems!.clear()
            
        fi
        
        if event!.getKeyCode() = 301 then
            rem up
            o = #selectedSuggestionItem
            n = max(0,o-1)
            if n<>o then
                item! = #suggestionItems!.get(o)
                item!.deselectItem()
                item! = #suggestionItems!.get(n)
                item!.selectItem()
                #selectedSuggestionItem = n
            fi
        fi
        if event!.getKeyCode() = 302 then
            rem down
            o = #selectedSuggestionItem
            n = min(#suggestionItems!.size()-1,o+1)
            if n<>o then
                item! = #suggestionItems!.get(o)
                item!.deselectItem()
                item! = #suggestionItems!.get(n)
                item!.selectItem()
                #selectedSuggestionItem = n
            fi
            
        fi
        
    methodend
    
    method public void onEditModify(BBjEditModifyEvent event!)
        if event!.getText().isEmpty() then
            if #suggestionsWindow! = null() then 
                methodret
            endif
            #suggestionsWindow!.destroy()
            #suggestionsWindow! = null()
            methodret
        endif
        BBjAPI().createTimer(500,#time!,#this!,"onTimerTimeout")
    methodend
    
    method public void onTimerTimeout(BBjTimerEvent event!)
        #fireEvent(#ON_FETCH_SUGGESTIONS,#inputE!.getText())
        BBjAPI().removeTimer(500)
    methodend
    
    method private void createSuggestions(ResultSet suggestions!)
        declare QuickSearchSuggestionItem suggestionItem!
        
        #suggestionsWindow! = #window!.addChildWindow(#window!.getAvailableControlID(),0,0,0,0,"",$00100800$,BBjAPI().getSysGui().getAvailableContext())
        #suggestionsWindow!.setPanelStyle("height",#suggestionHeight!)
        #suggestionsWindow!.setPanelStyle("overflow","auto")
        #suggestionsWindow!.setDockStyle("grid-column-start","1")
        #suggestionsWindow!.setDockStyle("display","grid")
        #suggestionsWindow!.setDockStyle("z-index","99")
        
        if #iconWrapper! <> null() then 
            #suggestionsWindow!.setDockStyle("grid-column-end","3")
        endif
        
        #suggestionsWindow!.setDockStyle("background-color","white")
        #suggestionsWindow!.setDockStyle("border","1px solid grey")
        
        
        if suggestions!.size() = 0 then 
            methodret
        endif    
        #buildQuickSearchItems(suggestions!)
    methodend
    
    method private void updateSuggestions(ResultSet suggestions!)
        declare QuickSearchSuggestionItem suggestionItem!
        
        if #suggestionItems!.size() > 0 then
            for i = 0 to #suggestionItems!.size() -1 
                declare QuickSearchSuggestionItem oldItem!
                oldItem! = cast(QuickSearchSuggestionItem,#suggestionItems!.get(i))
                oldItem!.destroy()
            next i
            #suggestionItems!.clear()
        endif
        
        if suggestions!.size() = 0 then 
            methodret
        endif
        
        #buildQuickSearchItems(suggestions!)
    methodend
    
    method private void buildQuickSearchItems(ResultSet suggestions!)
        for i = 0 to suggestions!.size() -1 
            suggestionItem! = new QuickSearchSuggestionItem(#suggestionsWindow!,#suggestionsWindow!.getAvailableControlID(),i)
            suggestionItem!.setCallback(QuickSearchSuggestionItem.ON_SUGGESTION_ITEM_CLICK,#this!,"onSuggestionItemClick")
            suggestionItem!.setData(suggestions!.get(i),#suggestionMask!)
            #suggestionItems!.add(suggestionItem!)
            if i = 0 
                suggestionItem!.selectItem()
            endif
        next i
        #selectedSuggestionItem = 0
    methodend
    
    method public void setSuffixIcon(BBjString path!)
        if #iconWrapper! = null() then 
            #iconWrapper! = #window!.addChildWindow(#window!.getAvailableControlID(),0,0,0,0,"",$00100800$,BBjAPI().getSysGui().getAvailableContext())
            #iconWrapper!.setDockStyle("grid-column-start","2")
            #iconWrapper!.setDockStyle("grid-row-start","1")
            #iconWrapper!.setDockStyle("height","fit-content")
            #iconWrapper!.setDockStyle("width","fit-content")
            #iconWrapper!.setDockStyle("align-self","center")
            #iconWrapper!.setCallback(BBjAPI.ON_MOUSE_DOWN,#this!,"onSuffixIconClick")
        endif
        #icon! = #iconWrapper!.addImageCtrl(#window!.getAvailableControlID(),0,0,0,0,path!)
    methodend
    
    method public void onSuffixIconClick(BBjMouseDownEvent event!)
        #fireEvent(#ON_SUFFIX_CLICK,"")
    methodend
    
    method public void onSuggestionItemClick(BBjCustomEvent event!)
        declare DataRow data!
        declare BBjNumber index!
        declare QuickSearchSuggestionItem item!
        
        index! = cast(BBjNumber,event!.getObject())
        item! = cast(QuickSearchSuggestionItem,#suggestionItems!.get(index!))
REM         item!.selectItem()
        data! = item!.getData()
        
        #fireEvent(#ON_ITEM_SELECT,data!)
        #selectedItem! = data!
        
        #suggestionsWindow!.destroy()
        #suggestionsWindow! = null()
        #suggestionItems!.clear()
    methodend
    
    method public void setSuggestions(ResultSet suggestions!)
        REM TODO
        if #suggestionsWindow! <> null() then 
            #updateSuggestions(suggestions!)
        else 
            #createSuggestions(suggestions!)
        endif
    methodend
    
    method public void setSuggestionMask(BBjString mask!)
        rem \[{2}\w*\]{2}
        #suggestionMask! = mask!
    methodend
    
    method public void setTimeoutTime(BBjNumber time!)
        #time! = time!
    methodend
    
    method public void setSuggestionHeight(BBjString height!)
        #suggestionHeight! = height!
    methodend
    
    method public BBjInputE getInputField()
        methodret #inputE!
    methodend
    
    method public BBjChildWindow getIconWrapper()
        methodret #iconWrapper!
    methodend
    
    method public BBjImageCtrl getIconControl()
        methodret #icon!
    methodend
    
    method public DataRow getSelectedItem()
        methodret #selectedItem!
    methodend
classend

class public QuickSearchSuggestionItem extends BBjWebWidget

    field private BBjChildWindow window!
    field private BBjStaticText text!
    field private BBjString content! = ""
    field private DataRow dr!
    field public static BBjNumber ON_SUGGESTION_ITEM_CLICK = 700004
    field private Boolean selected! = 0
    field private BBjNumber index!

    method public QuickSearchSuggestionItem(BBjChildWindow wnd!)
        #super!.create(wnd!)
    methodend

    method public QuickSearchSuggestionItem(BBjWindow wnd!, BBjInt id!)
        #super!.create(wnd!,id!)
    methodend

    method public QuickSearchSuggestionItem(BBjWindow wnd!, BBjInt id!, BBjNumber index!)
        #super!.create(wnd!,id!)
        #index! = index!
    methodend

    method public void redraw(Boolean init!)
        if init!
            #window! = #getCanvas()
            #window!.setPanelStyle("cursor","pointer")
            #window!.setTabTraversable(1)
            #window!.setCallback(BBjAPI.ON_MOUSE_DOWN,#this!,"onSuggestionItemClick")
            #text! = #window!.addStaticText(#window!.getAvailableControlID(),0,0,0,0,#content!)
            #text!.setStyle("margin-left","3px")
        else
            #text!.setText(#content!)
        FI
    methodend

    method public void onSuggestionItemClick(BBjMouseDownEvent event!)
        #fireEvent(#ON_SUGGESTION_ITEM_CLICK,#index!)
    methodend

    method public void setData(DataRow dr!, BBjString mask!)
        declare Pattern pattern!
        declare Matcher matcher!
        declare BBjVector fields!
        
        #dr! = dr!
        fields! = new BBjVector()
        pattern! = Pattern.compile("(\[{2}\w*\]{2})")
        matcher! = pattern!.matcher(mask!)
        while matcher!.find()
            declare BBjString field!

            field! = matcher!.group()
            field! = field!.replaceAll("\[","")
            field! = field!.replaceAll("\]","")
            fields!.add(field!)
        wend

        #content! = mask!.replaceAll("\[","")
        #content! = #content!.replaceAll("\]","")
        
        for i = 0 to fields!.size() -1
            #content! = #content!.replaceAll(str(fields!.get(i)),cvs(#dr!.getFieldAsString(str(fields!.get(i))),3))
        next i
        #redraw(0)
    methodend

    method public DataRow getData()
        methodret #dr!
    methodend

    method public void destroy()
        if !#window!.isDestroyed()
            #window!.destroy()
        FI
    methodend

    method public void selectItem()
        #selected! = 1
        #text!.setStyle("color","var(--bbj-color-white)",err=*next)
        #window!.setStyle("background-color","var(--bbj-color-primary-50)",err=*next)
    methodend

    method public void deselectItem()
        #selected! = 0
        #text!.setStyle("color","var(--bbj-color-black-50)",err=*next)
        #window!.setStyle("background-color","var(--bbj-color-white)",err=*next)
    methodend
classend