use ::BBjWidget/BBjWidget.bbj::BBjWidget
use ::WebKit/util/DynamicLoader.bbj::DynamicLoader

use ::WebKit/model/Icon.bbj::Icon

class public ListTile extends BBjWidget implements Icon

    field private BBjStaticText textField!
    field private BBjStaticText letterField!
    field private BBjString text!
    field private BBjString letter!
    field private BBjVector colors!
    field private BBjChildWindow btn!
    field private BBjChildWindow tile!
    field private BBjChildWindow iconWrapper!
    field private BBjStaticText trailingIcon!
    field private BBjString arrowDown! = "http://localhost:8888/files/WebKit/ArrowDown.png"
    field private BBjString arrowRight! = "http://localhost:8888/files/WebKit/ArrowRight.png"
    field private BBjStaticText arrowIcon!
    field private BBjStaticText starIcon!  
    field private BBjStaticText dotIcon!
    field private Boolean expanded! = Boolean.FALSE
    field private Boolean favorite! = Boolean.FALSE
    field private Boolean header! = Boolean.FALSE

    method public ListTile(BBjWindow wnd!)
        DynamicLoader.addLocalCSS("WebKit/widgets/ListTile/ListTile.css")
        #super!.create(wnd!,wnd!.getAvailableControlID())  
    methodend

    method public ListTile(BBjWindow wnd!, BBjInt id!)
        DynamicLoader.addLocalCSS("WebKit/widgets/ListTile/ListTile.css") 
        #super!.create(wnd!,id!)       
    methodend
    
    

    method public void redraw(Boolean f_init!)       
        #tile! = #getCanvas()
        
        if f_init!>0 then
            declare BBjChildWindow starWrapper!
            declare BBjChildWindow dotWrapper!
            
            #tile!.addPanelStyle("menutile")
            #tile!.setStyle("height","-moz-fit-content")
            #tile!.setCallback(BBjAPI.ON_MOUSE_ENTER,#this!,"mouseEnter")
            #tile!.setCallback(BBjAPI.ON_MOUSE_EXIT,#this!,"mouseExit")
            
            #btn! = #tile!.addChildWindow(#tile!.getAvailableControlID(),0,0,200,22,"",$00108800$,BBjAPI().getSysGui().getAvailableContext())
            #btn!.setCallback(BBjAPI.ON_MOUSE_DOWN,#this!,"onClick")
            #btn!.addPanelStyle("btnPart")
            
            #arrowIcon! = #setIcon(#btn!,"play")
            #arrowIcon!.setStyle("align-self","center") 
            #arrowIcon!.addStyle("arrowIcon")   
            #arrowIcon!.setVisible(0)
            
            #letterField! = #btn!.addStaticText(#btn!.getAvailableControlID(),0,0,200,22,"")
            #letterField!.addStyle("letterField")
            #letterField!.setStyle("text-align","center")
            
            #textField! = #btn!.addStaticText(#btn!.getAvailableControlID(),0,0,200,22,"")
            #textField!.addStyle("tilelabel")
            
            #iconWrapper! = #tile!.addChildWindow(#tile!.getAvailableControlID(),0,0,200,22,"",$00108800$,BBjAPI().getSysGui().getAvailableContext())
            #iconWrapper!.setStyle("margin-left","auto")
            #iconWrapper!.setStyle("display","flex")
            #iconWrapper!.setStyle("flex-direction", "row")
            #iconWrapper!.setVisible(0)
            
            starWrapper! = #iconWrapper!.addChildWindow(#iconWrapper!.getAvailableControlID(),0,0,200,22,"",$00108800$,BBjAPI().getSysGui().getAvailableContext())
            #starIcon! = #setIcon(starWrapper!,"star")
            starWrapper!.setCallback(BBjAPI.ON_MOUSE_DOWN,#this!,"onStarClick")
            starWrapper!.addStyle("trailingIcon")
            
            dotWrapper! = #iconWrapper!.addChildWindow(#iconWrapper!.getAvailableControlID(),0,0,200,22,"",$00108800$,BBjAPI().getSysGui().getAvailableContext())
            #dotIcon! = #setIcon(dotWrapper!,"more-vertical")
            dotWrapper!.setCallback(BBjAPI.ON_MOUSE_DOWN,#this!,"onDotsClick")
            dotWrapper!.addStyle("trailingIcon")
        FI
    methodend
    
    method public void setHeader()
        #header! = Boolean.TRUE
        #textField!.setStyle("font-weight","bold")
        #arrowIcon!.setVisible(1)
        #letterField!.setVisible(0)       
    methodend
    
    method public void setLetterColor(String color!)
        #letterField!.setStyle("background",color!)
    methodend
     
    method public void setText(BBjString text!)
        #text!= text!
        #textField!.setText(#text!)
        #letter!=text!.substring(0,1)
        #letterField!.setText(#letter!)
    methodend
    
    method public void mouseEnter(BBjMouseEnterEvent ev!)
        if !#header! then
            #iconWrapper!.setVisible(1)
            #iconWrapper!.setStyle("display","flex")
        endif
        #tile!.addStyle("hovered")
    methodend
    
    method public void mouseExit(BBjMouseExitEvent ev!)
        if !#header! then
            #iconWrapper!.setVisible(0)
        endif
        #tile!.removeStyle("hovered")
    methodend
   
    method public void onClick(BBjMouseDownEvent ev!)   
        
        if #expanded! then 
            #arrowIcon!.removeStyle("rotate")  
        else 
            #arrowIcon!.addStyle("rotate")   
        endif
        #expanded! = !#expanded!
        #super!.fireEvent(BBjAPI.ON_BUTTON_PUSH,ev!.getControl().getParentWindow().getID())
    methodend
    
    method public void onStarClick(BBjMouseDownEvent ev!)   
        if #favorite! then 
            #starIcon!.removeStyle("favoriteActive")
        else 
            #starIcon!.addStyle("favoriteActive")
        endif
        #favorite! = !#favorite!
    methodend
    
    method public void onDotsClick(BBjMouseDownEvent ev!)   
    methodend
    
classend