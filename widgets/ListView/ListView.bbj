use ::WebKit/util/DynamicLoader.bbj::DynamicLoader
use ::WebKit/util/ClientUtil.bbj::ClientUtil
use ::BBjWidget/BBjWidget.bbj::BBjWidget
use ::WebKit/model/ListView/ListViewDataModel.bbj::ListViewDataModel
use ::WebKit/model/Icon.bbj::Icon
use com.basiscomponents.db.ResultSet
use com.basiscomponents.db.DataRow

class public ListView extends BBjWidget implements Icon
    field private BBjChildWindow window!

    field private ResultSet RS!

    field private ListViewDataModel listDataModel!

    field public static BBjNumber ON_EDIT_PRESSED = 1234

    method public ListView(BBjWindow parent!)
        DynamicLoader.addLocalCSS("WebKit/widgets/ListView/ListView.css")
        #super!.create(parent!, parent!.getAvailableControlID())
    methodend

    method public ListView(BBjWindow parent!, ResultSet RS!, ListViewDataModel listDataModel!)
        DynamicLoader.addLocalCSS("WebKit/widgets/ListView/ListView.css")
        #RS! = rs!
        #listDataModel! = listDataModel!
        #super!.create(parent!, parent!.getAvailableControlID())
    methodend

    method public void setResultSet(ResultSet rs!)
        #RS! = rs!
        #redraw(1)
    methodend

    method public void setListDataModel(ListViewDataModel listDataModel!)
        #listDataModel! = listDataModel!
        #redraw(1)
    methodend

    method public BBjString getProperColumnWidth(BBjString columnWidth$, BBjNumber deduction)
        methodret " calc(" + columnWidth$ + " - " + str(deduction) + "px)"
    methodend


    method public void redraw(Boolean init!)
        if #listDataModel! = null() THEN
            REM do nothing
            methodret
        fi

        if #RS! = null() THEN
            REM do nothing
            methodret
        fi
        #window! = #getCanvas()

        rows! = #RS!.getDataRows()
        listWrapper! = #window!.addChildWindow(#window!.getAvailableControlID(),0,0,0,0,"",$00100800$,BBjAPI().getSysGui().getAvailableContext())
        listWrapper!.addPanelStyle("listWrapperPanelStyle")

        columnMappings! = #listDataModel!.getColumnMappings()
        numOfColumns = columnMappings!.size() 

        columnHeader! = listWrapper!.addChildWindow(listWrapper!.getAvailableControlID(),0,0,0,0,"",$00100800$,BBjAPI().getSysGui().getAvailableContext())
        columnHeader!.addPanelStyle("columnHeaderPanel")

        gridTemplateColumns$ = ""
        ClientUtil.consoleLog(str(#listDataModel!.getActionColumnStatus()))
        deduction = 0
        actionColumnWidth = 246
        actionColumnStatus! = #listDataModel!.getActionColumnStatus() 
        if actionColumnStatus! = 1 THEN
            deduction = actionColumnWidth/numOfColumns
        fi

        for i = 0 to numOfColumns - 1
            columnMapping! = columnMappings!.get(i)
            columnName! =columnHeader!.addStaticText(columnHeader!.getAvailableControlID(),0,0,0,0, columnMapping!.getColumnDisplayName())
            columnName!.addStyle("columnName")
            columnName!.setStyle("grid-column", str(columnMapping!.getColumnPosition()))
            gridTemplateColumns$ = gridTemplateColumns$ + #getProperColumnWidth(columnMapping!.getColumnWidth(), deduction)
        NEXT i

        REM render action column header
        actionColumnName! =columnHeader!.addStaticText(columnHeader!.getAvailableControlID(),0,0,0,0, "Actions")
        actionColumnName!.addStyle("columnName")
        actionColumnName!.addStyle("actionColumnName")
        actionColumnName!.setStyle("grid-column", str(numOfColumns + 1))
        gridTemplateColumns$ = gridTemplateColumns$ + str(actionColumnWidth) + "px"

        columnHeader!.setPanelStyle("grid-template-columns", gridTemplateColumns$)

        for i = 0 to rows!.size() - 1
            dataRowWindow! = listWrapper!.addChildWindow(listWrapper!.getAvailableControlID(),0,0,0,0,"",$00100800$,BBjAPI().getSysGui().getAvailableContext())
            dataRowWindow!.addPanelStyle("dataRowPanel")
            dataRowWindow!.setPanelStyle("grid-template-columns", gridTemplateColumns$)

            dataObject! = rows!.get(i)

            for j = 0 to numOfColumns - 1
                columnMapping! = columnMappings!.get(j)
                cellStructure! = columnMapping!.getCellStructure()
                _fields! = cellStructure!.getFields()
                cellWrapper! = dataRowWindow!.addChildWindow(dataRowWindow!.getAvailableControlID(),0,0,0,0,"",$00100800$,BBjAPI().getSysGui().getAvailableContext())
                cellWrapper!.addStyle("cellWrapper")
                cellWrapper!.setStyle("grid-column", str(columnMapping!.getColumnPosition()))
                cellWrapper!.addPanelStyle("cellWrapperPanel")

                declare BBjString primaryColumnValue$ 

                for k = 0 to _fields!.size() - 1
                    _fieldValue! = _fields!.get(k)
                    currentFieldValue$ = str(dataObject!.getFieldValue(_fieldValue!.getDataColumnAlias()))

                    if _fieldValue!.getIsPrimaryColumn() = 1 THEN
                        primaryColumnValue$ = currentFieldValue$ 
                    fi

                    _field! = cellWrapper!.addStaticText(cellWrapper!.getAvailableControlID(),0,0,0,0, currentFieldValue$)
                    _field!.addStyle("field")
                    customCssClass$ = _fieldValue!.getCustomCssClass()
                    if customCssClass$ = ""
                        _field!.addStyle("fieldLabel")
                    else
                        _field!.addStyle(customCssClass$)
                    fi
                    _field!.setStyle("grid-row", str(k + 1))
                NEXT k
                if j = 0 THEN
                    actionItem! = #setIcon(cellWrapper!, "dots-vertical")
                    actionItem!.addStyle("mobile-ellipsis-menu")
                fi
            NEXT j

            REM render action cells
            if actionColumnStatus! <> 1 THEN
                continue
            fi
            actionCellWrapper! = dataRowWindow!.addChildWindow(dataRowWindow!.getAvailableControlID(),0,0,0,0,"",$00100800$,BBjAPI().getSysGui().getAvailableContext())
            actionCellWrapper!.addStyle("cellWrapper actionCellWrapper")
            actionCellWrapper!.setStyle("grid-column", str(numOfColumns + 1))
            actionCellWrapper!.addPanelStyle("actionCellWrapperPanel")

            editActionWrapper! = actionCellWrapper!.addChildWindow(actionCellWrapper!.getAvailableControlID(), 0,0,0,0,"",$00100800$,BBjAPI().getSysGui().getAvailableContext())
            editActionWrapper!.setText(primaryColumnValue$)
            editActionItem! = #setIcon(editActionWrapper!, "edit")
            editActionWrapper!.setCallback(BBjAPI().ON_MOUSE_DOWN, #this!, "onEditPressed")

            clipboardActionWrapper! = actionCellWrapper!.addChildWindow(actionCellWrapper!.getAvailableControlID(), 0,0,0,0,"",$00100800$,BBjAPI().getSysGui().getAvailableContext())
            clipboardActionItem! = #setIcon(clipboardActionWrapper!, "clipboard")
            clipboardActionWrapper!.setCallback(BBjAPI().ON_MOUSE_DOWN, #this!, "onClipboardPressed")

            dotActionWrapper! = actionCellWrapper!.addChildWindow(actionCellWrapper!.getAvailableControlID(), 0,0,0,0,"",$00100800$,BBjAPI().getSysGui().getAvailableContext())
            dotActionItem! = #setIcon(dotActionWrapper!, "dots-vertical")
            dotActionWrapper!.setCallback(BBjAPI().ON_MOUSE_DOWN, #this!, "onDotsPressed")
        NEXT i
    methodend

    method public void onEditPressed(BBjMouseDownEvent ev!)
        REM ClientUtil.consoleLog(str(ev!.getControl().getText()))
        id$ = str(ev!.getControl().getText())
        #fireEvent(#ON_EDIT_PRESSED, id$)
    methodend

    method public void onClipboardPressed(BBjMouseDownEvent ev!)
        ClientUtil.consoleLog(str(ev!.getControl()))
    methodend
    method public void onDotsPressed(BBjMouseDownEvent ev!)
        ClientUtil.consoleLog(str(ev!.getControl()))
    methodend
classend
