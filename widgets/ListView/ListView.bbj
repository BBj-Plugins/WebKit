use ::WebKit/util/DynamicLoader.bbj::DynamicLoader
use ::WebKit/util/ClientUtil.bbj::ClientUtil
use ::BBjWidget/BBjWidget.bbj::BBjWidget
use ::WebKit/model/ListView/ListViewModel.bbj::ListViewModel
use ::WebKit/widgets/ListView/components/ListHeader/ListHeader.bbj::ListHeader
use com.basiscomponents.db.ResultSet
use com.basiscomponents.db.DataRow

class public ListView extends BBjWidget
    field private BBjChildWindow window!

    field private ResultSet RS!

    field private ListViewModel listDataModel!

    method public ListView(BBjWindow parent!, ResultSet RS!, ListViewModel listDataModel!)
        DynamicLoader.addLocalCSS("WebKit/widgets/ListView/ListView.css")
        #window! = parent!.addChildWindow(parent!.getAvailableControlID(),0,0,0,0,"",$00100800$,BBjAPI().getSysGui().getAvailableContext())

        #RS! = rs!
        #listDataModel! = listDataModel!
        #redraw(1)
    methodend

    method public BBjString getProperColumnWidth(BBjString columnWidth$, BBjNumber deduction)
        methodret " calc(" + columnWidth$ + " - " + str(deduction) + "px)"
    methodend


    method public void redraw(Boolean init!)
        listHeader! = new ListHeader(#window!, "Customer")

        rows! = #RS!.getDataRows()
        listWrapper! = #window!.addChildWindow(#window!.getAvailableControlID(),0,0,0,0,"",$00100800$,BBjAPI().getSysGui().getAvailableContext())
        listWrapper!.addPanelStyle("listWrapperPanelStyle")

        columnMappings! = #listDataModel!.getColumnMappings()
        numOfColumns = columnMappings!.size() 

        columnHeader! = listWrapper!.addChildWindow(listWrapper!.getAvailableControlID(),0,0,0,0,"",$00100800$,BBjAPI().getSysGui().getAvailableContext())
        columnHeader!.addPanelStyle("columnHeaderPanel")

        gridTemplateColumns$ = ""
        ClientUtil.consoleLog(str(#listDataModel!.getActionColumnStatus()))
        deduction = 0
        actionColumnWidth = 246
        actionColumnStatus! = #listDataModel!.getActionColumnStatus() 
        if actionColumnStatus! = 1 THEN
            deduction = actionColumnWidth/numOfColumns
        fi

        for i = 0 to numOfColumns - 1
            columnMapping! = columnMappings!.get(i)
            columnName! =columnHeader!.addStaticText(columnHeader!.getAvailableControlID(),0,0,0,0, columnMapping!.getColumnDisplayName())
            columnName!.addStyle("columnName")
            columnName!.setStyle("grid-column", str(columnMapping!.getColumnPosition()))
            gridTemplateColumns$ = gridTemplateColumns$ + #getProperColumnWidth(columnMapping!.getColumnWidth(), deduction)
        NEXT i

        REM render action column header
        actionColumnName! =columnHeader!.addStaticText(columnHeader!.getAvailableControlID(),0,0,0,0, "Actions")
        actionColumnName!.addStyle("columnName")
        actionColumnName!.addStyle("actionColumnName")
        actionColumnName!.setStyle("grid-column", str(numOfColumns + 1))
        gridTemplateColumns$ = gridTemplateColumns$ + str(actionColumnWidth) + "px"

        columnHeader!.setPanelStyle("grid-template-columns", gridTemplateColumns$)

        for i = 0 to rows!.size() - 1
            dataRowWindow! = listWrapper!.addChildWindow(listWrapper!.getAvailableControlID(),0,0,0,0,"",$00100800$,BBjAPI().getSysGui().getAvailableContext())
            dataRowWindow!.addPanelStyle("dataRowPanel")
            dataRowWindow!.setPanelStyle("grid-template-columns", gridTemplateColumns$)

            dataObject! = rows!.get(i)

            for j = 0 to numOfColumns - 1
                columnMapping! = columnMappings!.get(j)
                cellStructure! = columnMapping!.getCellStructure()
                _fields! = cellStructure!.getFields()
                cellWrapper! = dataRowWindow!.addChildWindow(dataRowWindow!.getAvailableControlID(),0,0,0,0,"",$00100800$,BBjAPI().getSysGui().getAvailableContext())
                cellWrapper!.addStyle("cellWrapper")
                cellWrapper!.setStyle("grid-column", str(columnMapping!.getColumnPosition()))
                cellWrapper!.addPanelStyle("cellWrapperPanel")

                for k = 0 to _fields!.size() - 1
                    _fieldValue! = _fields!.get(k)
                    _field! = cellWrapper!.addStaticText(cellWrapper!.getAvailableControlID(),0,0,0,0, dataObject!.getFieldValue(_fieldValue!.getDataColumnAlias()))
                    _field!.addStyle("field")
                    customCssClass$ = _fieldValue!.getCustomCssClass()
                    if customCssClass$ = ""
                        _field!.addStyle("fieldLabel")
                    else
                        _field!.addStyle(customCssClass$)
                    fi
                    _field!.setStyle("grid-row", str(k + 1))
                NEXT k
            NEXT j

            REM render action cells
            if actionColumnStatus! <> 1 THEN
                continue
            fi
            actionCellWrapper! = dataRowWindow!.addChildWindow(dataRowWindow!.getAvailableControlID(),0,0,0,0,"",$00100800$,BBjAPI().getSysGui().getAvailableContext())
            actionCellWrapper!.addStyle("cellWrapper actionCellWrapper")
            actionCellWrapper!.setStyle("grid-column", str(numOfColumns + 1))
            actionCellWrapper!.addPanelStyle("cellWrapperPanel")

            actionItem! = actionCellWrapper!.addStaticText(actionCellWrapper!.getAvailableControlID(),0,0,0,0, "M")
            actionItem!.addStyle("field")
        NEXT i


    methodend
classend
