REM /**
REM  * DashboardHeader.bbj
REM  * @author TimonGeisbauer
REM  *
REM  */

use ::BBjWidget/BBjWidget.bbj::BBjWidget
use ::WebKit/util/DynamicLoader.bbj::DynamicLoader
use ::WebKit/widgets/DashboardHeader/components/DashboardHeaderEntryWidget.bbj::DashboardHeaderEntryWidget
use ::WebKit/widgets/DashboardHeader/DashboardHeaderEntry.bbj::DashboardHeaderEntry

class public DashboardHeader extends BBjWidget
    
    field private BBjStaticText title!
    
    field private BBjStaticText subtitle!
    
    field private BBjStaticText headerText!
    
    field private BBjVector headerCards! = new BBjVector()
    
    field private BBjChildWindow icon!
    
    field private BBjChildWindow window!
    
    field private BBjChildWindow textWrapper!
    
    field private BBjChildWindow entryWrapper!
    
    method public DashboardHeader(BBjWindow wnd!)
        DynamicLoader.addLocalCSS("WebKit/widgets/DashboardHeader/DashboardHeader.css")
        #super!.create(wnd!,wnd!.getAvailableControlID())
    methodend
    
    method public DashboardHeader(BBjWindow wnd!, BBjInt id!)
        DynamicLoader.addLocalCSS("WebKit/widgets/DashboardHeader/DashboardHeader.css")
        #super!.create(wnd!,id!)
    methodend
    
    method public void redraw(Boolean init!)
        if !init! then 
            methodret
        endif 
        
        #window! = #getCanvas()
        #window!.addPanelStyle("dashboardHeaderPanelStyle")
        #window!.setDockStyle("width","100%")
        #window!.setDockStyle("height","100%")
        #window!.setStyle("width","100%")
        #window!.setStyle("height","100%")
        
        #textWrapper! = #window!.addChildWindow(#window!.getAvailableControlID(),0,0,0,0,"",$00100800$,BBjAPI().getSysGui().getAvailableContext())
        #textWrapper!.addPanelStyle("dashboardTextHeaderPanelStyle")
        
        #title! = #textWrapper!.addStaticText(#textWrapper!.getAvailableControlID(),0,0,0,0,"")
        #title!.addStyle("dashboardTitleStyle")
        
        #subtitle! = #textWrapper!.addStaticText(#textWrapper!.getAvailableControlID(),0,0,0,0,"")
        #subtitle!.addStyle("dashboardSubtitleStyle")
        
        #headerText! = #textWrapper!.addStaticText(#textWrapper!.getAvailableControlID(),0,0,0,0,"")
        #headerText!.addStyle("dashboardHeaderTextStyle")
    methodend
    
    method public void setEntrys(BBjVector vector!)
        declare BBjInt size!
        declare DashboardHeaderEntryWidget widget!
        declare auto DashboardHeaderEntry entryData!
        declare BBjString templateColumns! 
        
        templateColumns! = ""
        
        rem allow only 0-5 pieces
        if vector! = null() or vector!.isEmpty() then methodret
        size! = vector!.size()
        if vector!.size() > 5 then size! = 5
        
        if #entryWrapper! = null() then 
            #entryWrapper! = #window!.addChildWindow(#window!.getAvailableControlID(),0,0,0,0,"",$00100800$,BBjAPI().getSysGui().getAvailableContext())
            #entryWrapper!.addPanelStyle("dashboardHeaderEntryWrapperPanelStyle")
            rem #entryWrapper!.setDockStyle("grid-column-start","2")
            rem #entryWrapper!.setDockStyle("height","fit-content")
            rem #entryWrapper!.setDockStyle("width","fit-content")
            rem #entryWrapper!.setDockStyle("align-self","center")
            rem #entryWrapper!.setDockStyle("justify-self","end")
            rem #entryWrapper!.setDockStyle("margin-right","40px")
            #entryWrapper!.setDockStyle("flex","1")
        endif
        
        for i = 0 to size! -1
            widget! = new DashboardHeaderEntryWidget(#entryWrapper!)
            entryData! = vector!.get(i)
            widget!.setID(entryData!.getID())
            widget!.setLabel(entryData!.getText())
            widget!.setCount(entryData!.getCount())
            #headerCards!.add(widget!)
            rem templateColumns! = templateColumns! + "194px "
        next i
        rem #entryWrapper!.setPanelStyle("grid-template-columns",templateColumns!)
    methodend
    
    method public void updateCount(BBjInt id!, BBjInt count!)
        declare auto DashboardHeaderEntryWidget widget!
        for i = 0 to #headerCards!.size() -1 
            widget! = #headerCards!.get(i)
            if widget!.getID() = id! then 
                widget!.setCount(count!)
            endif
        next i
    methodend
    
    method public void setTextValues(BBjString title!, BBjString subtitle!, BBjString headerText!)
        #setTitle(title!)
        #setSubtitle(subtitle!)
        #setHeaderText(headerText!)
    methodend
    
    method public void setTitle(BBjString value!)
        #title!.setText(value!)
    methodend
    
    method public void setSubtitle(BBjString value!)
        #subtitle!.setText(value!)
    methodend
    
    method public void setHeaderText(BBjString value!)
        #headerText!.setText(value!)
    methodend
classend