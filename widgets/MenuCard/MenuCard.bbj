use ::BBjWidget/BBjWidget.bbj::BBjWidget
use ::WebKit/util/DynamicLoader.bbj::DynamicLoader
use ::WebKit/util/Icons.bbj::Icons
use ::WebKit/model/Menu.bbj::MenuItem
use ::WebKit/widgets/ListTile/ListTile.bbj::ListTile
use ::WebKit/util/Icons.bbj::Icons


class public MenuCard extends BBjWidget 

    field private BBjChildWindow card!
    field private MenuItem item!
    field private ListTile header!
    field private BBjStaticText body!
    field private BBjChildWindow footer!
    
    method public MenuCard(BBjWindow wnd!)
        DynamicLoader.addLocalCSS("WebKit/widgets/MenuCard/MenuCard.css")
        #super!.create(wnd!,wnd!.getAvailableControlID())
    methodend
    
    method public MenuCard(BBjWindow wnd!, BBjInt id!)
        DynamicLoader.addLocalCSS("WebKit/widgets/MenuCard/MenuCard.css")
        #super!.create(wnd!,id!)
    methodend
    
    method public void redraw(Boolean f_init!)
        #card! = #getCanvas()

        if f_init!>0 then
            #card!.addStyle("menuCard")
            
            #card!.setCallback(BBjAPI.ON_MOUSE_DOWN,#this!,"onClick")   
            #header! = new ListTile(#card!,#card!.getAvailableControlID())
            #header!.setCallback(ListTile.ON_FAVORITES_BUTTON_CLICK,#this!,"setFavorite")
            #header!.disableHover()
            #header!.setStyle("padding-left","25px")
         
            #body! = #card!.addStaticText(#card!.getAvailableControlID(),0,0,400,120,"")            
            #body!.addStyle("body")          
            #footer! = #card!.addChildWindow(#card!.getAvailableControlID(),0,0,200,25,"",$00108800$,BBjAPI().getSysGui().getAvailableContext())
            #footer!.addStyle("footer")
            #buildFooter()
        FI
    methodend
    
    method public void buildFooter()
        declare BBjCheckBox checkbox!
        declare BBjStaticText externalIcon!
     
        checkbox! = #footer!.addCheckBox(#footer!.getAvailableControlID(),0,0,22,22,"Open in new tab")
        checkbox!.addStyle("checkbox")
        externalIcon! = #footer!.addStaticText(#footer!.getAvailableControlID(),0,0,20,20,Icons.get("external-link"))
        
    methodend
    
    method public void setMenuItem(MenuItem item!)
        #item! = item!
        #header!.setText(item!.getCaption())
        if item!.getIcon() <> "" AND item!.getIcon() <> null() then
           #header!.setIcon(item!.getIcon())
        endif
        #body!.setText(item!.getToolTip())
    methodend
    
    method public void setHeader(BBjString headerText!)
        #header!.setText(headerText!)
    methodend
    
    method public void setBody(BBjString bodyText!)
        #body!.setText(bodyText!)
    methodend
    
    method public void setIcon(BBjString icon!)
        #header!.setIcon(icon!)
    methodend
    
    method public void setLetterColor(String color!)
        #header!.setLetterColor(color!)
    methodend
    
    method public void onClick(BBjMouseDownEvent ev!)   
        #super!.fireEvent(BBjAPI.ON_MOUSE_DOWN,ev!.getControl().getID())
    methodend
    
    method public void setFavorite(BBjCustomEvent event!)
    
    methodend

classend