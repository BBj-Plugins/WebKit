rem /**
rem  * BBj icons helper class
rem  *
rem  */
use java.util.Formatter
use ::WebKit/util/ClientUtil.bbj::ClientUtil

class public Icons

    field private static String LabelStyle! = "display:flex;align-items: center;gap: 1rem;"

    method public static String get(String iconName!)

        methodret #get (null(),iconName!)
    methodend
    
    method public static String get(String poolName!, String iconName!)
        methodret String.format("<html>%s</html>", #build(poolName!, iconName!,null()))
    methodend

    method public static String getWithStyling(String iconName!, String style!) 

    	methodret #get(null(),iconName!,style!)
    methodend   
    method public static String get(String poolName!, String iconName!, String style!)
        methodret String.format("<html>%s</html>", #build(poolName!, iconName!,style!))
    methodend

    method private static Formatter build(String poolName!, String iconName!, String style!)
        fmt! = new Formatter()

        if poolName! = null() then
            poolName! = STBL("icon-pool", err=*next)
        fi

        if poolName! = null() then
            if style! <> null() then
                methodret fmt!.format("<bbj-icon name='%s' style='%s'></bbj-icon>", iconName!,style!)
            endif
            methodret fmt!.format("<bbj-icon name='%s'></bbj-icon>", iconName!)
        fi

        if style! <> null() then 
            methodret fmt!.format("<bbj-icon pool='%s' name='%s' style='%s'></bbj-icon>", poolName!, iconName!, style!)
        endif
        
        methodret fmt!.format("<bbj-icon pool='%s' name='%s'></bbj-icon>", poolName!, iconName!)
    methodend

    method public static String getWithLabel(String poolName!, String iconName!, String label!)
        methodret #buildWithLabel(poolName!, iconName!, label!)
    methodend

    method public static String getWithLabel(String iconName!, String label!)
        methodret #buildWithLabel(null(), iconName!, label!)
    methodend

    method private static String buildWithLabel(String poolName!, String iconName!, String label!)
        icon! = #build(poolName!, iconName!,null())
        methodret String.format("<html><div class='BBjIconLabel' style='%s'>%s%s</div></html>", #LabelStyle!, icon!, label!)
    methodend

    method public static BBjStaticText getIconOnWindow(BBjWindow window!, String icon!)
        methodret #getIconOnWindow(window!, null(), icon!)             
    methodend

    method public static BBjStaticText getIconOnWindow(BBjWindow window!, String poolName!, String icon!)
        methodret window!.addStaticText(window!.getAvailableControlID(),0,0,0,0, #get(poolName!, icon!))        
    methodend

    method public static BBjStaticText getIconOnWindow(BBjWindow window!, String poolName!, String icon!, String style!)
        methodret window!.addStaticText(window!.getAvailableControlID(),0,0,0,0, #get(poolName!, icon!, style!))        
    methodend

classend
